# Symboles:  ➥ ➽ ⚠ ● »

options:
	
	lg_prefix: &d&lLG &6&l»
	lg_prefix_2: &6&l» &d&lLoup-Garou

	lg_permission: lg.admin
	lg_permission_message: &cVous n'avez pas accès à cette commande.

	lg_message_setwait: &eVous avez correctement défini la salle d'attente à votre position.

	lg_message_addSpawn: &eVous avez correctement défini la position de spawn %size of {lg::partie::spawnPosition::*}% à votre position.

	lg_message_forcestart: &eVous avez correctement démarré la partie de force.
	lg_message_forcestart_error: &cLa partie est déjà en cours.

	lg_message_forcestop: &eVous avez correctement arrêté la partie de force.
	lg_message_forcestop_error: &cLa partie a déjà été arrêtée

	lg_message_debug_on: &eVous avez correctement &aactivé &ele mode debug.
	lg_message_debug_off: &eVous avez correctement &cdésactivé &ele mode debug.

	lg_message_setup: &eVous avez correctement mis en place la partie de LG.
	lg_message_setup_error: &cVous avez déjà effectué cette commande.

	lg_message_error_setup: &cVous devez effectué la commande &4/lg-admin setup &cavant.
	lg_message_error_start: &cLa partie est déjà en cours.
	lg_message_error_stop: &cLa partie est déjà arrêtée.



	lg_description_role_lg_alpha: Chaque nuit, vous devez voter pour l'élimination d'un joueur." and "&7Votre but est d'éliminer tous les villageois. Votre vote &ecompte double&7.
	lg_description_role_lg: Chaque nuit, vous devez &evoter pour l'élimination d'un joueur&7." and "&7Votre but est d'éliminer tous les villageois.
	lg_description_role_vm: Vous êtes un villageois normal, vous n'avez rien à faire." and "&7En revanche, si les Loup-Garous vous tue, vous &edeviendrez Loup-Garou&7.
	lg_description_role_villager: Vous n'avez aucune action spécifique, vous n'avez &eaucune faculté&7.
	lg_description_role_cupidon: Au début de la partie, vous devez sélectionnez deux joueurs qui seront &eamant &7durant" and "toute la partie. &7Si l'un des deux meurt, l'autre mourra également.
	lg_description_role_vovo: Chaque nuit, vous avez la possiblité de &econnaître le rôle d'un joueur&7.
	lg_description_role_vovo_aura: Chaque nuit, vous avez la possiblité de &econnaître l'aura d'un joueur&7." and "&7L'aura peut être ""Claire"", ""Obscure"" ou bien ""Inconnue"".
	lg_description_role_fou: Vous devez essayer de convaincre le &evillage de vous tuer&7." and "&7Si vous y arrivez, vous gagnerzz la partie, mais pensez à être malin ;)
	lg_description_role_tireur: Vous commencez la partie avec &edeux balles&7." and "&7Vous aurez la possiblité de tuer n'importe qui.
	lg_description_role_cdt: Au début de la partie, un joueur sera choisi aléatoirement." and "&7Si ce joueur est &etué par le village&7, il gagnera la partie.
	lg_description_role_doc: Vous avez la possiblité chaque nuit de &eprotéger une personne" and "&7pour qu'il soit protégé des Loup-Garous.
	lg_description_role_soso: Vous possédez &edeux potions&7. L'une permets de récussiter un joueur," and "&7l'autre permets de tuer un joueur. Vous ne pouvez les utiliser qu'une seule fois.

	lg_skull_crazy: {SkullOwner:{Id:""7e18508f-feb6-445b-8020-2c7cb79ace7c"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvNzFiZWNhY2VjODU4YTE5ODQ4ODYwYjI1MjJjMzI5MzZhZGYxNTg4YzFlZjQyMzc5NDAzZmVmODNkYTRiYiJ9fX0=""}]}}}
	lg_skull_witch: {SkullOwner:{Id:""7f92b3d6-5ee0-4ab6-afae-2206b9514a63"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvMjBlMTNkMTg0NzRmYzk0ZWQ1NWFlYjcwNjk1NjZlNDY4N2Q3NzNkYWMxNmY0YzNmODcyMmZjOTViZjlmMmRmYSJ9fX0=""}]}}}
	lg_skull_wolf: {SkullOwner:{Id:""9cd0e1fb-c1d6-4859-9392-13655a000118"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvMzY4ZDQzMTI5MzliYjMxMTFmYWUyOGQ2NWQ5YTMxZTc3N2Y4ZjJjOWZjNDI3NTAxY2RhOGZmZTNiMzY3NjU4In19fQ==""}]}}}
	lg_skull_wolf_alpha: {SkullOwner:{Id:""736e9be2-4984-4cbc-bbb6-9ab419bb55bc"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZjgzYTJhYTlkMzczNGI5MTlhYzI0Yzk2NTllNWUwZjg2ZWNhZmJmNjRkNDc4OGNmYTQzM2JiZWMxODllOCJ9fX0=""}]}}}
	lg_skull_heart: {SkullOwner:{Id:""94c9b15f-f65a-427d-a699-ad0d4d5489f6"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZjRlYWY4OTQyOGEzNjQ5YzI2ZWRjMWY3MWNjMTlmMjYzZTlmNGViMzFlZDE4Yzk3Njg2YWFjODJmNzY0MjQyIn19fQ==""}]}}}
	lg_skull_target: {SkullOwner:{Id:""9d58f311-003b-4d1d-8ba8-b678e182bab3"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvY2MxZGYwZGI4ZDQ3NzNjYzI1NWE2M2I4ZjBiMGU0Y2RkN2Q3NGMzYjRjNjc1MzYyMTY4NDYwYmM2N2JiMzAyZSJ9fX0=""}]}}}


function lg_menu_deco(p: player, t: text, i: integer = 6):
	open virtual chest inventory with {_i} rows named "%{_t}%" to {_p}
	if {_i} = 3:
		make a gui slot 1, 2, 3, 4, 5, 6, 7, 9, 10, 16, 17, 19, 20, 21, 22, 23, 24 and 25 of {_p} with magenta stained glass pane
		make a gui slot 0, 8, 18 and 26 of {_p} with purple stained glass pane
	if {_i} = 5:
		make a gui slot 1, 2, 3, 4, 5, 6, 7, 9, 10, 16, 17, 18, 26, 27, 28, 34, 35, 37, 38, 39, 40, 41, 42 and 43 of {_p} with magenta stained glass pane
		make a gui slot 0, 8, 36 and 44 of {_p} with purple stained glass pane
	if {_i} = 6:
		make a gui slot 1, 2, 3, 4, 5, 6, 7, 9, 10, 16, 17, 18, 26, 27, 35, 36, 37, 43, 44, 45, 46, 47, 48, 49, 50, 51 and 52 of {_p} with magenta stained glass pane
		make a gui slot 0, 8, 45 and 53 of {_p} with purple stained glass pane


function lg_menu_informations(p: player):
	lg_menu_deco({_p}, "&8➽ &dInformations du LG", 5)
	make a gui slot 21 of {_p} with {_p}'s skull named "&8» &6Liste des rôles" to run:
		lg_menu_roles({_p})
	make a gui slot 22 of {_p} with book named "&8» &bRègles du jeu" to run:
		close {_p}'s inventory
		lg_message_rules({_p})
	make a gui slot 23 of {_p} with emerald named "&8» &aCondition de victoire" to run:
		close {_p}'s inventory
		lg_message_victory({_p})


function lg_menu_roles(p: player):
	lg_menu_deco({_p}, "&8➽ &6Liste des rôles", 6)
	make a gui slot 19 of {_p} with {_p}'s skull with nbt "{@lg_skull_wolf_alpha}" named "&8» &4Loup-Garou Alpha" with lore "" and "&8➥ &7{@lg_description_role_lg_alpha}" and "" and "&8➥ &7Votre équipe: &cLoup-Garou"
	make a gui slot 20 of {_p} with {_p}'s skull with nbt "{@lg_skull_wolf}" named "&8» &cLoup-Garou" with lore "" and "&8➥ &7{@lg_description_role_lg}" and "" and "&8➥ &7Votre équipe: &cLoup-Garou"
	make a gui slot 21 of {_p} with skull of "MHF_Herobrine" parsed as offline player named "&8» &7Villageois &8Maudit" with lore "" and "&8➥ &7{@lg_description_role_vm}" and "" and "&8➥ &7Votre équipe: &6Village &8| &cLoup-Garou"
	make a gui slot 22 of {_p} with skull of "MHF_Villager" parsed as offline player named "&8» &7Villageois" with lore "" and "&8➥ &7{@lg_description_role_villager}" and "" and "&8➥ &7Votre équipe: &6Village"
	make a gui slot 23 of {_p} with {_p}'s skull with nbt "{@lg_skull_heart}" named "&8» &dCupidon" with lore "" and "&8➥ &7{@lg_description_role_cupidon}" and "" and "&8➥ &7Votre équipe: &6Village"
	make a gui slot 24 of {_p} with eyes of ender named "&8» &aVoyante" with lore "" and "&8➥ &7{@lg_description_role_vovo}" and "" and "&8➥ &7Votre équipe: &6Village"
	make a gui slot 25 of {_p} with ender pearl named "&8» &2Voyante Aura" with lore "" and "&8➥ &7{@lg_description_role_vovo_aura}" and "" and "&8➥ &7Votre équipe: &6Village"
	make a gui slot 28 of {_p} with potion named "&8» &bDocteur" with lore "" and "&8➥ &7{@lg_description_role_doc}" and "" and "&8➥ &7Votre équipe: &6Village"
	color slot 28 of current inventory of {_p} pink
	make a gui slot 29 of {_p} with {_p}'s skull with nbt "{@lg_skull_witch}" named "&8» &5Sorcière" with lore "" and "&8➥ &7{@lg_description_role_soso}" and "" and "&8➥ &7Votre équipe: &6Village"
	make a gui slot 30 of {_p} with bow named "&8» &eTireur" with lore "" and "&8➥ &7{@lg_description_role_tireur}" and "" and "&8➥ &7Votre équipe: &6Village"
	make a gui slot 31 of {_p} with {_p}'s skull with nbt "{@lg_skull_crazy}" named "&8» &6Fou" with lore "" and "&8➥ &7{@lg_description_role_fou}" and "" and "&8➥ &7Votre équipe: &aSolo"
	make a gui slot 32 of {_p} with {_p}'s skull with nbt "{@lg_skull_target}" named "&8» &9Chasseur de Tête" with lore "" and "&8➥ &7{@lg_description_role_cdt}" and "" and "&8➥ &7Votre équipe: &aSolo"
	make a gui slot 49 of {_p} with skull of "MHF_ArrowLeft" parsed as offline player named "&8➥ &cRevenir en arrière" to run:
		lg_menu_informations({_p})

function lg_message_victory(p: player):
	send "" to {_p}
	send "{@lg_prefix} &aCondition de victoire:" to {_p}
	send "  &f» &eTous les &6villagoies &esont mort" to {_p}
	send "  &f» &eTous les &6Loup-garou sont &emort" to {_p}
	send "  &f» &eLe &6Fou &eest tué par le village" to {_p}
	send "  &f» &eLa cible du &6chasseur de tête &eest tué par le village" to {_p}
	send "  &f» &eLes &6deux amants &eainsi que &6cupidon &esont les trois derniers survivant" to {_p}
	send "" to {_p}

function lg_message_rules(p: player):
	send "" to {_p}
	#send "{@lg_prefix} &aCondition de victoire:" to {_p}
	#send "  &f» &eTous les &6villagoies &esont mort" to {_p}

function lg_get_configuration(mode: text = "int") :: text:
	set {_p} to size of {lg::player::joueurs::*}
	if {_mode} is "sb":
		if {_p} is between 5 and 12:    # 5 à 12 donc |-3 +4
			return "&6Restrinte &7~8 joueurs"
		if {_p} is between 13 and 17:    # 13 à 17 | -2 +2
			return "&eClassique &7~15 joueurs"
		if {_p} is between 18 and 26:    # 18 à 26 | -4 +3
			return "&aEtendu &7~22 joueurs"
		if {_p} is greater than or equal to 27:
			return "&cJoueurs trop nombreux"
		else:
			return "&cJoueurs manquant"
	else:
		if {_p} is between 5 and 12:    # 5 à 12 donc |-3 +4
			return "1"
		if {_p} is between 13 and 17:    # 13 à 17 | -2 +2
			return "2"
		if {_p} is between 18 and 26:    # 18 à 26 | -4 +3
			return "3"
		if {_p} is greater than or equal to 27:
			return "4"
		else:
			return "0"

function lg_get_roles_configuration() :: objects:
	set {_p} to size of {lg::player::joueurs::*}
	set {_config} to lg_get_configuration({_p}) parsed as integer

	# - VERIFICATION
	if {_config} is 0:
		return "&cIl manque %5 - {_p}% joueurs avant de lancer une partie."
		stop
	if {_config} is 4:
		return "&cIl y a trop de joueurs pour lancer une partie."
		stop

	# - REPARTITION ROLES DE BASE
	add "Voyante", "Docteur" and "Tireur" to {_roles::*}    
	if {_config} is 1:
		{_p} is not 5 # Obligation pour avoir 1 LG / 1 VILLAGEOIS
		add "Fou" to {_roles::*}
	if {_config} is 2 or 3:
		add "Voyante Aura", "Cupidon", "Loup-Garou Alpha", "Villageois Maudit", "Villageois Maudit", "Sorcière" and "Chasseur de tête" to {_roles::*}

	# - ATTRIBUTION LG/VILLAGEOIS
	remove size of {_roles::*} from {_p}
	while {_p} is greater than 0:
		if mod({_p}, 2) is 0:
			add "Loup-Garou" to {_roles::*}
		else:
			add "Villageois" to {_roles::*} # Prioriser les villageois
		remove 1 from {_p}

	return {_roles::*}

function lg_give_roles():
	set {_n} to 0
	set {_roles::*} to lg_get_roles_configuration()
	loop {lg::player::joueurs::*}:
		set {lg::joueur::role::%loop-value%} to {_roles::%{_n}%}
		send loop-value title "{@lg_prefix_2}" with subtitle "&6➥ &eVous êtes désormais &a%{_roles::%{_n}%}%" for 2 seconds
		send "" to loop-value
		send "{@lg_prefix} &eVotre rôles:" to loop-value
		if {lg::joueur::role::%loop-value%} is "Loup-garou":
			send "&8» &7{@lg_description_role_lg}" to loop-value
		if {lg::joueur::role::%loop-value%} is "Loup-garou alpha":
			send "{@lg_description_role_lg_alpha}" to loop-value
		if {lg::joueur::role::%loop-value%} is "Voyante":
			send "{@lg_description_role_vovo}" to loop-value
		if {lg::joueur::role::%loop-value%} is "Voyante aura":
			send "{@lg_description_role_vovo_aura}" to loop-value
		if {lg::joueur::role::%loop-value%} is "Docteur":
			send "{@lg_description_role_doc}" to loop-value
		if {lg::joueur::role::%loop-value%} is "Tireur":
			send "{@lg_description_role_tireur}" to loop-value
		if {lg::joueur::role::%loop-value%} is "Villageois Maudit":
			send "{@lg_description_role_vm}" to loop-value
		if {lg::joueur::role::%loop-value%} is "Sorcière":
			send "{@lg_description_role_soso}" to loop-value
		if {lg::joueur::role::%loop-value%} is "Chasseur de tête":
			send "{@lg_description_role_cdt}" to loop-value
		if {lg::joueur::role::%loop-value%} is "Fou":
			send "{@lg_description_role_fou}" to loop-value
		if {lg::joueur::role::%loop-value%} is "Villageois":
			send "{@lg_description_role_villager}" to loop-value
		if {lg::joueur::role::%loop-value%} is "Cupidon":
			send "{@lg_description_role_cupidon}" to loop-value
		send "" to {_p}

		if {_roles::%{_n}%} contains "loup-garou":
			add {_p} to {lg::player::equipe::lg::*}


function lg_join(p: player):
	loop 200 times:
		send "" to {_p}
	set {_p}'s xp to 0
	teleport {_p} to {lg::partie::waitRoom}
	set {_p}'s gamemode to adventure
	clear {_p}'s inventory
	heal {_p}
	set current hotbar slot of {_p} to 4
	set slot 4 of {_p} to book named "&8➽ &dInformations &7(clic droit)"
	set slot 8 of {_p} to orange bed named "&8➽ &cQuitter la partie &7(clic droit)"
	lg_scoreboard({_p}, "setup")
	lg_setup()
	lg_setup_player({_p})
	lg_check_start()

function lg_quit(p: player):
	remove {_p} from {lg::player::joueurs::*}
	lg_scoreboard({_p}, "remove")

function lg_setup_player(p: player):
	add {_p} to {lg::player::joueurs::*}
	set {lg::player::role::%{_p}%} to "&7Aucune"
	set {lg::player::equipe::%{_p}%} to "&7Aucune"

function lg_setup():
	set {lg::partie::day} to 0
	set {lg::partie::status} to "&6Attente" #| "&6Attente" / "&aEn cours" / "&cArrêté"


function lg_time(t: integer, p: player, mode: text):
	
	loop ({_t} - 5) times:
		if {_mode} = "start":
			set action bar of {_p} to "&6➥ &eLa partie commence dans &a%{_t}% secondes"
		set {_p}'s level to {_t}
		if mod({_t}, 10) = 0: 
			if {_mode} = "start":
				send {_p} title "{@lg_prefix_2}" with subtitle "&6➥ &eLa partie commence dans &a%{_t}% secondes" for 2 seconds

		remove 1 from {_t}
		wait a second
	loop 5 times:
		if {_mode} = "start":
			send {_p} title "{@lg_prefix_2}" with subtitle "&6➥ &eLa partie commence dans &a%{_t}% secondes" for 2 seconds
			set action bar of {_p} to "&6➥ &eLa partie commence dans &a%{_t}% secondes"
		set {_p}'s level to {_t}
		remove 1 from {_t}
		wait a second
	if {_mode} = "start":
		send {_p} title "{@lg_prefix_2}" with subtitle "&6➥ &eLa partie commence !" for a second
		set action bar of {_p} to "&6➥ &eLa partie commence !"
	set {_p}'s level to 0


function lg_check_win():
	if size of {lg::player::joueurs::*} = 0:
		# reset de la partie
		lg_end()
	if size of {lg::player::joueurs::*} = 1:
		# player a win
		lg_end()

function lg_check_start(force: boolean = false):
	if {_force} is false:
		if (size of {lg::player::joueurs::*}) < 5:
			stop
	loop {lg::player::joueurs::*}:
		lg_time(30, loop-value, "start")
	wait 30 seconds
	lg_start()

function lg_start():
	set {lg::partie::start} to true
	set {lg::partie::status} to "&eEn cours"
	set {_spawn} to 1
	loop {lg::player::joueurs::*}:
		teleport loop-value to {lg::partie::spawnPosition::%{_spawn}%}
		set {lg::player::spawn::%loop-value%} to {_spawn}
		add 1 to {_spawn}
	lg_give_roles()
	wait 5 seconds
	set {lg::partie::status} to "&7Nuit"


function lg_end():
	loop {lg::player::joueurs::*}:
		kick loop-value due to "{@lg_prefix} &eLa partie est terminé."
	delete {lg::partie::start}
	clear {lg::player::*}
	lg_setup()

function lg_scoreboard(p: player, t: text):
	if {_t} is "setup":
		create a new stylish scoreboard named "%{_p}%"
		set title of stylish scoreboard "%{_p}%" to "{@lg_prefix_2}"
		create a new id based score "%{_p}%11" with text "&1" slot 11 for stylish scoreboard "%{_p}%"
		create a new id based score "%{_p}%10" with text "&6&l» &f&lPartie:" slot 10 for stylish scoreboard "%{_p}%"
		create a new id based score "%{_p}%9" with text "&2" slot 9 for stylish scoreboard "%{_p}%"
		create a new id based score "%{_p}%8" with text " &6● &eStatus &f» &e%{lg::partie::status}%" slot 8 for stylish scoreboard "%{_p}%"
		create a new id based score "%{_p}%7" with text " &6● &eConnectés &f» &e%size of {lg::player::joueurs::*}%" slot 7 for stylish scoreboard "%{_p}%"
		create a new id based score "%{_p}%6" with text " &6● &eConfigura&etion &f» &e%lg_get_configuration(""sb"")%" slot 6 for stylish scoreboard "%{_p}%"
		create a new id based score "%{_p}%5" with text " &6● &eJour &f» &bn°%{lg::partie::day}%" slot 5 for stylish scoreboard "%{_p}%"
		create a new id based score "%{_p}%4" with text "&3" slot 4 for stylish scoreboard "%{_p}%"
		create a new id based score "%{_p}%3" with text "&6&l» &f&lJoueur:" slot 3 for stylish scoreboard "%{_p}%"
		create a new id based score "%{_p}%2" with text "&4" slot 2 for stylish scoreboard "%{_p}%"
		create a new id based score "%{_p}%1" with text " &6● &eVotre rôle &f» &e%{lg::player::role::%{_p}%}%" slot 1 for stylish scoreboard "%{_p}%"
		create a new id based score "%{_p}%0" with text " &6● &eVotre équipe &f» &e%{lg::player::equipe::%{_p}%}%" slot 0 for stylish scoreboard "%{_p}%"
		set stylish scoreboard of {_p} to "%{_p}%"
		while {_p} is online:
			set the text of id "%{_p}%8" to " &6● &eStatus &f» &e%{lg::partie::status}%"
			set the text of id "%{_p}%7" to " &6● &eConnectés &f» &e%size of {lg::player::joueurs::*}%"
			set the text of id "%{_p}%6" to " &6● &eConfigura&etion &f» &e%lg_get_configuration(""sb"")%"
			set the text of id "%{_p}%5" to " &6● &eJour &f» &bn°%{lg::partie::day}%"
			set the text of id "%{_p}%1" to " &6● &eVotre rôle &f» &e%{lg::player::role::%{_p}%}%"
			set the text of id "%{_p}%0" to " &6● &eVotre équ&eipe &f» &e%{lg::player::equipe::%{_p}%}%"
			wait a second
	else:
		reset stylish scoreboard of {_p}
		delete stylish scoreboard named "%{_p}%"


function lg_settings(p: player):
	lg_menu_deco({_p}, "&6➽ &eParamètres du LG", 3)
	make a gui slot 11 of {_p} with painting named "&6» &eConfiguration de la partie" with lore "" and "&f➥ &d%lg_get_configuration(""sb"")%" and "&7(Dépends du nombre de joueurs connectés sur le serveur)"


# {lg::partie::*} -> paramètre de la partie
# {lg::player::*} -> contient tous ce qui concerne les joueurs (rôles etc...)


# {lg::partie::status} -> "&6Attente" / "&eEn cours" / "&7Nuit" / "&fJour" / "&aTerminé"
 
command /item:
	trigger:
		send "%player's tool%"
		send "%{lg::player::joueurs::*}%"

function lg_admin_help(p: player):
	send "" to {_p}
	send "{@lg_prefix} &eCommandes admin:" to {_p}
	send "  &f» &e/lg-admin help" to {_p}
	send "  &f» &e/lg-admin setwait" to {_p}
	send "  &f» &e/lg-admin addSpawn" to {_p}
	send "  &f» &e/lg-admin forcestart" to {_p}
	send "  &f» &e/lg-admin forcestop" to {_p}
	send "  &f» &e/lg-admin settings" to {_p}
	#send "  &f» &e/lg-admin debug" to {_p}
	send "" to {_p}


command /lg [<text>] [<text>] [<text>]:
	permission: {@lg_permission}
	permission message: {@lg_prefix} {@lg_permission_message}
	trigger:
		if arg 1 isn't set:
			lg_admin_help(player)
		if arg 1 is "help":
			lg_admin_help(player)
		if arg 1 is "setwait":
			if {lg::partie::start} is set:
				send "{@lg_prefix} {@lg_message_error_start}"
			else:
				set {lg::partie::waitRoom} to location of player
				send "{@lg_prefix} {@lg_message_setwait}"
		if arg 1 is "addSpawn":
			if {lg::partie::start} is set:
				send "{@lg_prefix} {@lg_message_error_start}"
			else:
				set {_size} to (size of {lg::partie::spawnPosition::*}) + 1
				set {lg::partie::spawnPosition::%{_size}%} to location of player
				send "{@lg_prefix} {@lg_message_addSpawn}"
		if arg 1 is "forcestart":
			if {lg::partie::start} is set:
				send "{@lg_prefix} {@lg_message_error_start}"
			else:
				set {lg::partie::start} to true
				lg_check_start(true)
		if arg 1 is "forcestop":
			if {lg::partie::start} isn't set:
				send "{@lg_prefix} {@lg_message_error_stop}"
			else:
				lg_end()
		if arg 1 is "settings":
			lg_settings(player)
		#if arg 1 is "debug":
		#	if {lg::partie::debug} isn't set:
		#		set {lg::partie::debug} to true
		#		send "{@lg_prefix} {@lg_message_debug_on}"
		#	else:
		#		delete {lg::partie::debug}
		#		send "{@lg_prefix} {@lg_message_debug_off}"


on chat:
	replace all "&" with "§" in message
	cancel event
	if {lg::partie::start} is set:
		if {lg::partie::status} is "&7Nuit":
			if {lg::player::role::%player%} is "Loup-garou" or "Loup-garou alpha":
				loop {lg::player::equipe::lg::*}:
					send "&8[&7Nuit&8] &8[&cLoup-garou&8] &7%player% &8» &7%message%" to loop-value
			else:
				send "{@lg_prefix} &cVotre rôle ne vous permet pas parler durant la nuit."
		else:
			broadcast "&8[&fJour&8] &7%player% &8» &7%message%"
	else:
		broadcast "&7%player% &8» &7%message%"



on join:
	if {lg::partie::start} is set:
		kick player due to "{@lg_prefix} &eUne partie est déjà en cours."
	else:
		set join message to "&a✔ %player% &7(%amount of all players% &8» %lg_get_configuration(""sb"")%&7)"
		lg_join(player)

on quit:
	set quit message to "&c✘ %player% &7(%amount of all players - 1% &8» %lg_get_configuration(""sb"")%&7)"
	lg_quit(player)


on right click:
	if name of player's tool is "&8➽ &cQuitter la partie &7(clic droit)":
		kick player due to "{@lg_prefix} &eVous avez correctement quitté la partie."
	if name of player's tool is "&8➽ &dInformations &7(clic droit)":
		lg_menu_informations(player)

on inventory click:
	if player isn't op:
		cancel event

on hunger meter change:
	cancel event

on weather change:
	cancel event

on damage of player:
	cancel event
