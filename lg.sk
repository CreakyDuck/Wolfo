import:
    java.util.ArrayList
    java.util.Collections
    java.util.Map$Entry
    ch.njol.skript.variables.Variables
 
expression replace values %strings% with %strings% in %string%:
    get:
        set {_string} to expression-3
        loop expressions-1:
            add 1 to {_index}
            replace all "%loop-value%" with "%{_index}th element of expressions-2%" in {_string}
        return {_string}
 
plural expression [the] (1¦(highest|top)|2¦(lowest|last)) %integer% values of %objects% [formatted] as %string%:
    get:
        set {_list} to new ArrayList(Variables.getVariable(raw expressions-2.getName().toString(event), event and raw expressions-2.isLocal()).entrySet())
        {_list}.sort(Entry.comparingByValue())
        if parse mark = 1:
            Collections.reverse({_list})
        loop ...{_list}:
            "%loop-value.getValue()%" is not "0"
            add 1 to {_index}
            set {_sorted::%{_index}%} to replace values "@index" and "@value" with "%loop-value.getKey()%" and "%loop-value.getValue()%" in expression-3
            {_index} = expression-1
            stop loop
        return {_sorted::*}

on script load:
	lg_yml()

function lg_yml():
	# Réunion des messages "general" et "roles" en un seul fichier, "messages"
	# Création d'un fichier de configuration, pour la "main"
	load yaml "plugins/Loup-Garou/messages.yml" as "messages"
	load yaml "plugins/Loup-Garou/config.yml" as "config"

	# Vérification de l'existence d'un fichier MESSAGES.YML
	if yaml value "GENERATED" from "messages" is not set:

		#	----------------- Messages généraux -----------------

		set yaml value "GENERATED" from "messages" to true
		set yaml value "PREFIX.GENERAL" from "messages" to "&d&lLG &6&l» &r"
		set yaml value "PREFIX.TITLE" from "messages" to "&6&l» &d&lLoup-Garou"
		set yaml value "PERMISSION.ADMIN" from "messages" to "lg.admin"
		set yaml value "MESSAGE.PERMISSION" from "messages" to "&cVous n'avez pas accès à cette commande."
		set yaml value "MESSAGE.SETWAITROOM" from "messages" to "&eVous avez correctement défini la salle d'attente à votre position."
		set yaml value "MESSAGE.ADDSPAWN" from "messages" to "&eVous avez correctement défini la position de spawn à votre position. "
		set yaml value "MESSAGE.FORCESTART" from "messages" to "&eVous avez correctement démarré la partie de force."
		set yaml value "MESSAGE.FORCESTOP" from "messages" to "&eVous avez correctement arrêté la partie de force."
		set yaml value "MESSAGE.DEBUGON" from "messages" to "&eVous avez correctement &aactivé &ele mode développement."
		set yaml value "MESSAGE.DEBUGOFF" from "messages" to "&eVous avez correctement &cdésactivé &ele mode développement."
		set yaml value "MESSAGE.YMLON" from "messages" to "&eVous avez correctement &aactivé &eles informations YML."
		set yaml value "MESSAGE.YMLOFF" from "messages" to "&eVous avez correctement &cdésactivé &eles informations YML."
		set yaml value "MESSAGE.SETUP" from "messages" to "&eVous avez correctement mis en place la partie de LG."
		set yaml value "MESSAGE.SETUPERROR" from "messages" to "&cVous avez déjà effectué cette commande."
		set yaml value "MESSAGE.ALREADYSTART" from "messages" to "&cLa partie est déjà en cours."
		set yaml value "MESSAGE.ALREADYSTOP" from "messages" to "&cLa partie est déjà arrêtée."
		set yaml value "MESSAGE.ALREADYSETUP" from "messages" to "&cLa partie est déjà configurée."
		set yaml value "MESSAGE.ENDGAME" from "messages" to "La partie est terminée."
		set yaml value "MESSAGE.ROLEDESC" from "messages" to "&eDescription de votre rôle :"
		set yaml value "MESSAGE.MUTED" from "messages" to "Votre rôle ne vous permet pas parler durant la nuit."
		set yaml value "MESSAGE.GOODLEFT" from "messages" to "Vous avez correctement quitté la partie."
		set yaml value "MESSAGE.NOTENOUGH1" of "messages" to "&cIl manque "
		set yaml value "MESSAGE.NOTENOUGH2" of "messages" to " &cjoueurs avant de lancer une partie."
		set yaml value "MESSAGE.TOOMANY" of "messages" to "&cIl y a trop de joueurs pour lancer une partie."
		set yaml value "MESSAGE.REVEAL" of "messages" to "&6➥ &eVous êtes désormais "
		set yaml value "MESSAGE.START" of "messages" to "&6➥ &eLa partie commence !"
		set yaml value "MESSAGE.STARTING" of "messages" to "&6➥ &eLa partie commence dans "
		set yaml value "MESSAGE.YMLRELOAD" of "messages" to "&eEtes-vous sûr de vouloir réinitialiser les messages ? Si oui, faites /lg ymlreload confirm."
		set yaml value "GUI.INFO_TITLE" from "messages" to "&8➽ &dInformations du LG"
		set yaml value "GUI.ROLELIST" from "messages" to "&8» &6Liste des rôles"
		set yaml value "GUI.ROLELIST_TITLE" from "messages" to "&8➽ &6Liste des rôles"
		set yaml value "GUI.RULES" from "messages" to "&8» &bRègles du jeu"
		set yaml value "GUI.VICTORY" from "messages" to "&8» &aCondition de victoire"
		set yaml value "ITEM.INFO" from "messages" to "&8➽ &dInformations &7(clic droit)"
		set yaml value "ITEM.EXIT" from "messages" to "&8➽ &cQuitter la partie &7(clic droit)"
		set yaml value "TIME.HOUR" from "messages" to "heure(s)"
		set yaml value "TIME.SECOND" from "messages" to "seconde(s)"
		# Messages CONDITION VICTOIRE
		set yaml value "MESSAGE.VICTORY_FIRST" from "messages" to "&aConditions de victoire :"
		add "  &f» &eTous les &6villagoies &esont mort", "  &f» &eTous les &6Loup-garou sont &emort", "  &f» &eLe &6Fou &eest tué par le village", "  &f» &eLa cible du &6chasseur de tête &eest tué par le village", "  &f» &eLes &6deux amants &eainsi que &6cupidon &esont les trois derniers survivant" and "" to {_victory::*}
		set yaml list "MESSAGE.VICTORY" from "messages" to {_victory::*}
		# Messages ADMIN
		set yaml value "MESSAGE.ADMIN_FIRST" from "messages" to "&eCommandes admin :"
		add "  &f» &e/lg-admin help", "  &f» &e/lg-admin setwait", "  &f» &e/lg-admin addSpawn", "  &f» &e/lg-admin forcestart", "  &f» &e/lg-admin forcestop", "  &f» &e/lg-admin settings" and "" to {_admin::*}
		set yaml list "MESSAGE.ADMIN" from "messages" to {_admin::*}
		# Loup-Garou
		set yaml value "WEREWOLF.NAME" from "messages" to "&8» &cLoup-Garou"
		set yaml value "WEREWOLF.DESC" from "messages" to "&8➥ &7Chaque nuit, vous devez voter pour l'élimination d'un joueur. &7Votre but est d'éliminer tous les villageois."
		set yaml value "WEREWOLF.HEAD" from "messages" to "{SkullOwner:{Id:""9cd0e1fb-c1d6-4859-9392-13655a000118"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvMzY4ZDQzMTI5MzliYjMxMTFmYWUyOGQ2NWQ5YTMxZTc3N2Y4ZjJjOWZjNDI3NTAxY2RhOGZmZTNiMzY3NjU4In19fQ==""}]}}}"
		# Loup-Garou Alpha
		set yaml value "WEREWOLF_ALPHA.NAME" from "messages" to "&8» &4Loup-Garou Alpha"
		set yaml value "WEREWOLF_ALPHA.DESC" from "messages" to "Votre vote &ecompte double&7."
		set yaml value "WEREWOLF_ALPHA.HEAD" from "messages" to "{SkullOwner:{Id:""736e9be2-4984-4cbc-bbb6-9ab419bb55bc"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZjgzYTJhYTlkMzczNGI5MTlhYzI0Yzk2NTllNWUwZjg2ZWNhZmJmNjRkNDc4OGNmYTQzM2JiZWMxODllOCJ9fX0=""}]}}}"
		# Villageois Maudit
		set yaml value "TOWNSFOLK_DAMN.NAME" from "messages" to "&8» &7Villageois &8Maudit"
		set yaml value "TOWNSFOLK_DAMN.DESC" from "messages" to "&8➥ &7Vous êtes un villageois normal, vous n'avez rien à faire. &7En revanche, si les Loup-Garous vous tue, vous &edeviendrez Loup-Garou&7."
		# Villageois
		set yaml value "TOWNSFOLK.NAME" from "messages" to "&8» &7Villageois"
		set yaml value "TOWNSFOLK.DESC" from "messages" to "&8➥ &7Vous n'avez aucune action spécifique, vous n'avez &eaucune faculté&7."
		# Cupidon
		set yaml value "CUPID.NAME" from "messages" to "&8» &dCupidon"
		set yaml value "CUPID.DESC" from "messages" to "&8➥ &7Au début de la partie, vous devez sélectionnez deux joueurs qui seront &eamant &7durant toute la partie. Si l'un des deux meurt, l'autre mourra également."
		set yaml value "CUPID.HEAD" from "messages" to "{SkullOwner:{Id:""94c9b15f-f65a-427d-a699-ad0d4d5489f6"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZjRlYWY4OTQyOGEzNjQ5YzI2ZWRjMWY3MWNjMTlmMjYzZTlmNGViMzFlZDE4Yzk3Njg2YWFjODJmNzY0MjQyIn19fQ==""}]}}}"
		# Voyante
		set yaml value "SEER.NAME" from "messages" to "&8» &aVoyante"
		set yaml value "SEER.DESC" from "messages" to "&8➥ &7Chaque nuit, vous avez la possiblité de &econnaître le rôle d'un joueur&7."
		# Voyante Aura
		set yaml value "SEER_AURA.NAME" from "messages" to "&8» &2Voyante Aura"
		set yaml value "SEER_AURA.DESC" from "messages" to "&8➥ &7Chaque nuit, vous avez la possiblité de &econnaître l'aura d'un joueur&7. L'aura peut être ""Claire"", ""Obscure"" ou bien ""Inconnue""."
		# Fou
		set yaml value "CRAZY.NAME" from "messages" to "&8» &6Fou"
		set yaml value "CRAZY.DESC" from "messages" to "&8➥ &7Vous devez essayer de convaincre le &evillage de vous tuer&7. Si vous y arrivez, vous gagnerez la partie, mais pensez à être malin ;)"
		set yaml value "CRAZY.HEAD" from "messages" to "{SkullOwner:{Id:""7e18508f-feb6-445b-8020-2c7cb79ace7c"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvNzFiZWNhY2VjODU4YTE5ODQ4ODYwYjI1MjJjMzI5MzZhZGYxNTg4YzFlZjQyMzc5NDAzZmVmODNkYTRiYiJ9fX0=""}]}}}"
		# Tireur
		set yaml value "SHOOTER.NAME" from "messages" to "&8» &eTireur"
		set yaml value "SHOOTER.DESC" from "messages" to "&8➥ &7Vous commencez la partie avec &edeux balles&7. Vous aurez la possiblité de tuer n'importe qui."

		# Chasseur de Tête
		set yaml value "HEADHUNTER.NAME" from "messages" to "&8» &9Chasseur de Tête"
		set yaml value "HEADHUNTER.DESC" from "messages" to "&8➥ &7Au début de la partie, un joueur sera choisi aléatoirement. Si ce joueur est &etué par le village&7, il gagnera la partie."
		set yaml value "HEADHUNTER.HEAD" from "messages" to "{SkullOwner:{Id:""9d58f311-003b-4d1d-8ba8-b678e182bab3"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvY2MxZGYwZGI4ZDQ3NzNjYzI1NWE2M2I4ZjBiMGU0Y2RkN2Q3NGMzYjRjNjc1MzYyMTY4NDYwYmM2N2JiMzAyZSJ9fX0=""}]}}}"
		# Docteur
		set yaml value "DOCTOR.NAME" from "messages" to "&8» &bDocteur"
		set yaml value "DOCTOR.DESC" from "messages" to "&8➥ &7Vous avez la possiblité chaque nuit de &eprotéger une personne contre les Loup-Garous."
		# Sorcière
		set yaml value "WITCH.NAME" from "messages" to "&8» &5Sorcière"
		set yaml value "WITCH.DESC" from "messages" to "&8➥ &7Vous possédez &edeux potions&7. L'une permets de récussiter un joueur, l'autre permets de tuer un joueur. Vous ne pouvez les utiliser qu'une seule fois."
		set yaml value "WITCH.HEAD" from "messages" to "{SkullOwner:{Id:""7f92b3d6-5ee0-4ab6-afae-2206b9514a63"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvMjBlMTNkMTg0NzRmYzk0ZWQ1NWFlYjcwNjk1NjZlNDY4N2Q3NzNkYWMxNmY0YzNmODcyMmZjOTViZjlmMmRmYSJ9fX0=""}]}}}"
		# Camp (WEREWOLF, VL, WEREWOLF_VL, SOLO)
		set yaml value "TEAM.WEREWOLF" from "messages" to "&8➥ &7Votre équipe: &cLoup-Garou"
		set yaml value "TEAM.TOWNSFLOK" from "messages" to "&8➥ &7Votre équipe: &6Village"
		set yaml value "TEAM.WW_TF" from "messages" to "&8➥ &7Votre équipe: &6Village &8| &cLoup-Garou"
		set yaml value "TEAM.SOLO" from "messages" to "&8➥ &7Votre équipe: &aSolo"
		save yaml "messages"
	if yaml value "GENERATED" from "config" is not set:
		set yaml value "GENERATED" from "config" to true
		set yaml value "ANIMATION.SELECTION" from "config" to "ARROW"
		save yaml "config"

function msg(request: text) :: text: # POUR LES TEXTES (plus utilisé)
	broadcast "&8[YAML &7MSG&8]: &8REQ:&7%{_request}% &f- &8TYPE:&7TEXT" if {lg::partie::debug::yml} is true
	return yaml value {_request} from "messages"

function msgB(request: text) :: boolean: # POUR LES BOOLEAN
	broadcast "&8[YAML &7MSG&8]: &8REQ:&7%{_request}% &f- &8TYPE:&7BOOLEAN" if {lg::partie::debug::yml} is true
	return yaml value {_request} from "messages"

function msgO(request: text) :: objects: # POUR LES OBJECTS
	broadcast "&8[YAML &7MSG&8]: &8REQ:&7%{_request}% &f- &8TYPE:&7OBJECTS" if {lg::partie::debug::yml} is true
	return yaml list {_request} from "messages"

# Symboles:  ➥ ➽ ⚠ ● »

function lg_help(msg: text, p: player):
	send join msg("PREFIX.GENERAL") and msg("MESSAGE.%{_msg}%_FIRST") to {_p}
	set {_%{_msg}%::*} to msgO("MESSAGE.%{_msg}%")
	loop {_%{_msg}%::*}:
		send colored loop-value to {_p}

function lg_scoreboard(p: player, t: text):
	if {_t} is "setup":
		create a new stylish scoreboard named "%{_p}%"	
		set title of stylish scoreboard "%{_p}%" to msg("PREFIX.TITLE")
		create a new id based score "%{_p}%11" with text "&1" slot 11 for stylish scoreboard "%{_p}%"	
		create a new id based score "%{_p}%10" with text "&6&l» &f&lPartie:" slot 10 for stylish scoreboard "%{_p}%"	
		create a new id based score "%{_p}%9" with text "&2" slot 9 for stylish scoreboard "%{_p}%"	
		create a new id based score "%{_p}%8" with text " &6● &eStatus &f» &e%{lg::partie::status}%" slot 8 for stylish scoreboard "%{_p}%"	
		create a new id based score "%{_p}%7" with text " &6● &eConnectés &f» &e%size of {lg::player::joueurs::*}%" slot 7 for stylish scoreboard "%{_p}%"	
		create a new id based score "%{_p}%6" with text " &6● &eConfigura&etion &f» &e%lg_get_configuration(1)%" slot 6 for stylish scoreboard "%{_p}%"	
		create a new id based score "%{_p}%5" with text " &6● &eJour &f» &bn°%{lg::partie::day}%" slot 5 for stylish scoreboard "%{_p}%"	
		create a new id based score "%{_p}%4" with text "&3" slot 4 for stylish scoreboard "%{_p}%"	
		create a new id based score "%{_p}%3" with text "&6&l» &f&lJoueur:" slot 3 for stylish scoreboard "%{_p}%"	
		create a new id based score "%{_p}%2" with text "&4" slot 2 for stylish scoreboard "%{_p}%"	
		create a new id based score "%{_p}%1" with text " &6● &eVotre rôle &f» &e%{lg::player::role::%{_p}%}%" slot 1 for stylish scoreboard "%{_p}%"	
		create a new id based score "%{_p}%0" with text " &6● &eVotre équipe &f» &e%{lg::player::equipe::%{_p}%}%" slot 0 for stylish scoreboard "%{_p}%"	
		set stylish scoreboard of {_p} to "%{_p}%"
		while {_p} is online:
			set the text of id "%{_p}%8" to " &6● &eStatus &f» &e%{lg::partie::status}%"	
			set the text of id "%{_p}%7" to " &6● &eConnectés &f» &e%size of {lg::player::joueurs::*}%"	
			set the text of id "%{_p}%6" to " &6● &eConfigura&etion &f» &e%lg_get_configuration(1)%"	
			set the text of id "%{_p}%5" to " &6● &eJour &f» &bn°%{lg::partie::day}%"	
			set the text of id "%{_p}%1" to " &6● &eVotre rôle &f» &e%{lg::player::role::%{_p}%}%"	
			set the text of id "%{_p}%0" to " &6● &eVotre équ&eipe &f» &e%{lg::player::equipe::%{_p}%}%"	
			wait a second
	else:	
		reset stylish scoreboard of {_p}
		delete stylish scoreboard named "%{_p}%"

function lg_get_configuration(mode: integer) :: text:
	set {_p} to size of {lg::player::joueurs::*}
	if {_mode} is 1:
		if {_p} is between 5 and 12:    # 5 à 12 donc |-3 +4
			return "&6Mini" # ou Restreinte
		if {_p} is between 13 and 17:    # 13 à 17 | -2 +2
			return "&eClassique"
		if {_p} is between 18 and 26:    # 18 à 26 | -4 +3
			return "&aMéga"
		else:
			return "&cJoueurs manquants"
	else if {_mode} is 2:
		if {_p} is between 5 and 12:    # 5 à 12 donc |-3 +4
			return "1"
		if {_p} is between 13 and 17:    # 13 à 17 | -2 +2
			return "2"
		if {_p} is between 18 and 26:    # 18 à 26 | -4 +3
			return "3"
		if {_p} is greater than or equal to 27:
			return "4"
		else:
			return "0"

function lg_get_roles_configuration() :: objects:
	set {_p} to size of {lg::player::joueurs::*}
	set {_config} to lg_get_configuration(2) parsed as integer
	# - VERIFICATION
	if {_config} is 0:
		return join msg("MESSAGE.NOTENOUGH1"), "&c%5 - {_p}%" and msg("MESSAGE.NOTENOUGH2")
		stop
	# - REPARTITION ROLES DE BASE
	add "SEER", "DOCTOR" and "SHOOTER" to {_roles::*}    
	if {_config} is 1:
		{_p} is not 5 # Obligation pour avoir 1 LG / 1 VILLAGEOIS
		add "CRAZY" to {_roles::*}
	if {_config} is 2 or 3:
		add "SEER_AURA", "CUPID", "WEREWOLF_ALPHA", "TOWNSFOLK_DANM", "TOWNSFOLK_DANM", "WITCH" and "HEADHUNTER" to {_roles::*}
	# - ATTRIBUTION LG/VILLAGEOIS
	remove size of {_roles::*} from {_p}
	while {_p} is greater than 0:
		if mod({_p}, 2) is 0:
			add "WEREWOLF" to {_roles::*}
		else:
			add "TOWNSFOLK" to {_roles::*}
		remove 1 from {_p}
	return {_roles::*}

function lg_give_roles():
	set {_input::*} to lg_get_roles_configuration() 
	if lg_get_configuration(2) parsed as integer is 0 or 4:
		broadcast join msg("PREFIX.GENERAL") and {_input::*}
		stop
	lg_round(0)
	loop {lg::player::joueurs::*}:
		set {lg::player::role::%loop-value%} to {_input::%loop-index%}
		send title msg("PREFIX.TITLE") with subtitle join msg("MESSAGE.REVEAL") and "&a%{_input::%loop-index%}%" to loop-value for 2 seconds
		send join msg("PREFIX.GENERAL"), msg("MESSAGE.REVEAL") and {_input::%loop-index%} to loop-value
		send msg("%{_input::%loop-index%}%.DESC") to loop-value
		if {_input::%loop-index%} contains "WEREWOLF":
			add {_p} to {lg::player::equipe::lg::*}
		wait 3 ticks # 5 joueurs / seconde (si 4 ticks) --> maximum 5 secondes d'attente avec une partie complète
	lg_round(1)
	
function lg_round(type: integer):
	if {_type} is 0:
		set {lg::partie::day} to 0
		set {lg::partie::start} to true
		set {lg::partie::status} to "&6Jour"
	if {_type} is 1:
		{lg::partie::start} is true
		if {lg::partie::status} is "&6Jour":
			while "%time of ""world""%" isn't "0:00":
				add 50 ticks to time of "world"
				wait a tick
			strike lightning at {lg::partie::center}
			set {lg::partie::status} to "&7Nuit"
			lg_vote(0)
		else:
			while "%time of ""world""%" isn't "12:00":
				add 50 ticks to time of "world"
				wait a tick
			set {lg::partie::status} to "&6Jour"
			add 1 to {lg::partie::day}
			lg_vote(1)

function lg_vote(i: number):
	if {_i} is 0:
		if {lg::partie::day} is 0:
			broadcast "&6[TEST]&f Vote de Cupidon ..."
			wait 1 second
		broadcast "&6[TEST]&f Vote des LG ..."
		wait 1 second
		broadcast "&6[TEST]&f Voyante, Voyante Aura, Docteur ..."
		wait 1 second
		broadcast "&6[TEST]&f Vote de la Sorcière ..."
		wait 1 second
		broadcast "&6[TEST]&f Fin des votes !"
		broadcast "&6[TEST]&f XXX a été tué !"
		wait 1 second
	else:
		broadcast "&6[TEST]&f Vote des joueurs ..."
		wait 5 seconds
	lg_round(1)

command /test [<text>]:
	trigger:
		lg_draw(player, arg-1)

function lg_draw(p: player, type: text):
	if {_type} is "erase":
		stopEffect id "lg-center"
		stopEffect id "lg-selected"
		stopEffect id "lg-rayon"
		stopEffect id "lg-sphere"
		stopEffect id "lg-spawn"
	if {_type} is "selected":
		stopEffect id "lg-%{_type}%"
		set {_loc} to location at x-coordinate of {_p}, y-coordinate of {_p} + 2,  z-coordinate of {_p} in world "world"
		if {lg::player::spawn::%{_p}%} is 3 or 4 or 5 or 9 or 10 or 11 or 15 or 16 or 17 or 21 or 22 or 23:
			set {_angle} to 45
		if {lg::player::spawn::%{_p}%} is 6 or 7 or 8 or 18 or 19 or 20:
			set {_angle} to 90
		else:
			set {_angle} to 0
		if yaml value "ANIMATION.SELECTION" from "config" is "HAND":
			drawImage file "hand.png", center {_loc}, id "lg-%{_type}%", pixelStepX 2, pixelStepY 2, scale 8, visibleRange 50, xR 0, yR {_angle}, zR 0, disX 0, disY 4, disZ 0, tps 0, second 2
		else: # Pour quand même afficher une flèche si la config est erronée
			drawImage file "arrow.png", center {_loc}, id "lg-%{_type}%", pixelStepX 2, pixelStepY 2, scale 12, visibleRange 50, xR 0, yR {_angle}, zR 0, disX 0, disY 4, disZ 0, tps 0, second 2

		# AFFICHER UN HOLO AU DESSUS DE LA TETE DU JOUEUR AVEC SON NOMBRE DE VOTE -> {lg::player::vote::%{_p}%}
	if {_type} is "kill":
		set {_loc} to {lg::partie::center}
		add 3 to y-coordinate of {_loc}
		set {_pos} to location of {_p}
		add 1 to y-coordinate of {_pos}
		drawArc particle "flame", center location of {_pos}, target location of {_loc}, id "lg-rayon", rainbowMode false, density 50, height 0, pitchMultiplier 0, visibleRange 30
		set {_r} to 0
		loop 20 times:
			stopEffect id "lg-sphere"
			drawSphere style 1, particle "flame", center location of {_pos}, id "lg-sphere", rainbowMode false, radius {_r}, density 400, visibleRange 32, pulseDelay 1
			add 0.1 to {_r}
			wait a tick
		wait 8 ticks
		hide {_p} to all players
		strike lightning effect at location of {_p}
		wait 2 ticks
		stopEffect id "lg-rayon"
		stopEffect id "lg-sphere"
	if {_type} is "spawn":
		hide {_p} to all players
		drawWarpRings style 2, particle "redstone", RGB 80, 255, 255, center location of {_p}, id "lg-%{_type}%", rainbowMode true, scan true, height 2, radius 1, ringCount 40, ringDensity 20, visibleRange 32, pulseDelay 1
		wait 2 second
		stopEffect id "lg-%{_type}%"
		reveal {_p} to all players
	if {_type} is "center":
		drawAtom particle1 "redstone", particle2 "flame", center {lg::partie::center}, id "lg-%{_type}%", rainbowMode true, innerPCount 10, innerRadius .5, outerPCount 2, orbitCount 5, start 0, visibleRange 30, rotation 15

function lg_most_voted() :: objects:
	set {_playerVotes::*} to the highest (size of {lg::player::vote::*}) values of {lg::player::vote::*} as "@index;@value"
	loop {_playerVotes::*}:
		set {_s::*} to loop-value split at ";"
		add {_s::1} to {_players::*}
		add {_s::2} to {_votes::*}
	if {_votes::1} = {_votes::2}:
		chance of 50%:                                 # Si les deux premiers joueurs on le même nombre de vote,
			return {_players::1} and {_votes::1}       # 50/50 pour savoir qui va mourir.
		else:
			return {_players::2} and {_votes::2}
	return {_players::1} and {_votes::1}

function lg_join(p: player):
	loop 200 times:
		send "" to {_p}
	set {_p}'s xp to 0
	teleport {_p} to {lg::partie::waitRoom}
	set {_p}'s gamemode to adventure
	clear {_p}'s inventory
	heal {_p}
	set current hotbar slot of {_p} to 4
	set slot 4 of {_p} to book named msg("ITEM.INFO")
	set slot 8 of {_p} to orange bed named msg("ITEM.EXIT")
	lg_scoreboard({_p}, "setup")
	lg_setup()
	lg_setup_player({_p})
	lg_check_start()

function lg_quit(p: player):
	remove {_p} from {lg::player::joueurs::*}
	lg_scoreboard({_p}, "remove")

function lg_setup_player(p: player):
	add {_p} to {lg::player::joueurs::*}
	set {lg::player::role::%{_p}%} to "&7Aucune"
	set {lg::player::equipe::%{_p}%} to "&7Aucune"

function lg_setup():
	set {lg::partie::day} to 0
	set {lg::partie::status} to "&6Attente" #| "&6Attente" / "&aEn cours" / "&cArrêté"

function lg_time(t: integer, p: player, mode: text):
	# Mise en place des variables, afin de ne pas faire de requêtes au fichier YML toutes les secondes
	if {_mode} = "start":
		set {_second} to msg("TIME.SECOND")
		set {_title} to msg("MESSAGE.TITLE")
		set {_starting} to msg("MESSAGE.STARTING")
		set {_start} to msg("MESSAGE.START")

	loop ({_t} - 5) times:
		if {_mode} = "start":
			send action bar join {_starting}, "&a%{_t}% " and {_second} to {_p}
		set {_p}'s level to {_t}
		if mod({_t}, 10) = 0: 
			if {_mode} = "start":
				send title {_title} with subtitle join {_starting}, "&a%{_t}% " and {_second} to {_p} for 2 seconds

		remove 1 from {_t}
		wait a second
	loop 5 times:
		if {_mode} = "start":
			send title {_title} with subtitle join {_starting}, "&a%{_t}% " and {_second} to {_p} for 2 seconds
			send action bar join {_starting}, "&a%{_t}% " and {_second} to {_p}
		set {_p}'s level to {_t}
		remove 1 from {_t}
		wait a second
	if {_mode} = "start":
		send title {_title} with subtitle {_start} to {_p} for a second
		send action bar {_start} to {_p}
	set {_p}'s level to 0

function lg_check_win():
	if size of {lg::player::joueurs::*} = 0:
		# reset de la partie
		lg_end()
	if size of {lg::player::joueurs::*} = 1:
		# player a win
		lg_end()

function lg_check_start(force: boolean = false):
	if {_force} is false:
		if (size of {lg::player::joueurs::*}) < 5:
			stop
	loop {lg::player::joueurs::*}:
		lg_time(30, loop-value, "start")
	wait 30 seconds
	lg_start()

function lg_start():
	set {_spawn} to 1
	loop {lg::player::joueurs::*}:
		teleport loop-value to {lg::partie::spawnPosition::%{_spawn}%}
		add 1 to {_spawn}
	lg_give_roles()

function lg_end():
	loop {lg::player::joueurs::*}:
		kick loop-value due to join msg("PREFIX.GENERAL") and msg("MESSAGE.ENDGAME")
	clear {lg::partie::start}
	clear {lg::player::*}
	clear {lg::partie::day}
	lg_setup()

function lg_settings(p: player):
	lg_menu_deco({_p}, "&6➽ &eParamètres du LG", 3)
	make a gui slot 11 of {_p} with painting named "&6» &eConfiguration de la partie" with lore "" and "&f➥ &d%lg_get_configuration(1)%" and "&7(Dépends du nombre de joueurs connectés sur le serveur)"
	
command /lg [<text>] [<text>] [<text>]:
	permission: msg("PERMISSION.ADMIN")
	permission message: join msg("PREFIX.GENERAL") and msg("MESSAGE.PERMISSION")
	trigger:
		if arg 1 isn't set:
			lg_help("ADMIN", player)
		if arg 1 is "help":
			lg_help("ADMIN", player)
		if arg 1 is "setwait":
			if {lg::partie::start} is set:
				send join msg("PREFIX.GENERAL") and msg("MESSAGE.ALREADYSTART")
			else:
				set {lg::partie::waitRoom} to location of player
				send join msg("PREFIX.GENERAL") and msg("MESSAGE.SETWAITROOM")
		if arg 1 is "addSpawn":
			if {lg::partie::start} is set:
				send join msg("PREFIX.GENERAL") and msg("MESSAGE.ALREADYSTART")
			else:
				set {_size} to (size of {lg::partie::spawnPosition::*}) + 1
				set {lg::partie::spawnPosition::%{_size}%} to location of player
				send join msg("PREFIX.GENERAL"), msg("MESSAGE.ADDSPAWN") and "&7(%size of {lg::partie::spawnPosition::*}%)"
		if arg 1 is "forcestart":
			if {lg::partie::start} is set:
				send join msg("PREFIX.GENERAL") and msg("MESSAGE.ALREADYSTART")
			else:
				set {lg::partie::start} to true
				lg_check_start(true)
		if arg 1 is "forcestop":
			if {lg::partie::start} isn't set:
				send join msg("PREFIX.GENERAL") and msg("MESSAGE.ALREADYSTOP")
			else:
				lg_end()
		if arg 1 is "settings":
			lg_settings(player)
		if arg 1 is "debug":
			if arg 2 is "yml":
				if {lg::partie::debug::yml} isn't set:
					set {lg::partie::debug::yml} to true
					send join msg("PREFIX.GENERAL") and msg("MESSAGE.YMLON")
				else:
					delete {lg::partie::debug::yml}
					send join msg("PREFIX.GENERAL") and msg("MESSAGE.YMLOFF")
			else if arg 2 is not set:
				if {lg::partie::debug} isn't set:
					set {lg::partie::debug} to true
					send join msg("PREFIX.GENERAL") and msg("MESSAGE.DEBUGON")
				else:
					delete {lg::partie::debug}
					send join msg("PREFIX.GENERAL") and msg("MESSAGE.DEBUGOFF")
		if arg 1 is "ymlreload": # ou autre x)
			if arg 2 is not "confirm":
				send join msg("PREFIX.GENERAL") and msg("MESSAGE.YMLRELOAD")
			else:
				set yaml value "GENERATED" of "messages" to false
				save yaml "messages"
				lg_yml()
				
on chat:
	replace all "&" with "§" in message
	cancel event
	if {lg::partie::start} is set:
		if {lg::partie::status} is "&7Nuit":
			if {lg::player::role::%player%} is "Loup-garou" or "Loup-garou alpha":
				loop {lg::player::equipe::lg::*}:
					send "&8[&7Nuit&8] &8[&cLoup-garou&8] &7%player% &8» &7%message%" to loop-value
			else:
				send join msg("PREFIX.GENERAL") and msg("MESSAGE.MUTED")
		else:
			broadcast "&8[&fJour&8] &7%player% &8» &7%message%"
	else:
		broadcast "&7%player% &8» &7%message%"

on join:
	if {lg::partie::start} is not set:
		kick player due to join msg("PREFIX.GENERAL") and msg("MESSAGE.ALREADYSTART")
	else if size of {lg::player::joueurs::*} is greater than or equal to 27:
		kick player due to join msg("PREFIX.GENERAL") and msg("MESSAGE.TOOMANY")
	else:
		set join message to "&a✔ %player% &7(%amount of all players% &8» %lg_get_configuration(1)%&7)"
		lg_join(player)

on quit:
	set quit message to "&c✘ %player% &7(%amount of all players - 1% &8» %lg_get_configuration(1)%&7)"
	lg_quit(player)


on right click:
	if name of player's tool is msg("ITEM.INFO"):
		lg_menu_informations(player)
	else if name of player's tool is msg("ITEM.EXIT"):
		kick player due to join msg("PREFIX.GENERAL") and msg("MESSAGE.GOODLEFT")

on inventory click:
	if player isn't op:
		cancel event

on hunger meter change:
	cancel event

on weather change:
	cancel event

on damage of player:
	cancel event
