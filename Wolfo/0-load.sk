# Ce script contient toutes les informations nécessaires pour faire fonctionner les autres scripts.
# Merci de ne pas modifier son nom afin de garder une hiérachie et charger les autres scripts convenablement.

on script load:
	wolfoInitialize()

function wolfoInitialize():
	load yaml "plugins/Skript/scripts/Wolfo/config.yml" as "config"
	if yaml "config" is empty:
		send "&4Oops! The file containing the config could not be found." to console
		send "&7Please download it and add it to the Wolfo folder." to console
		add 1 to {_error}
	set {_actual} to text from "https://files.romitou.fr/wolfo"
	set {_current} to wolfoGet("VERSION")
	if {_actual} is not {_current}:
		send "&6Wow! It's been a while since you last updated Wolfo." to console
		send "&7Version &eV%{_actual}%&7 is already out, you are in &eV%{_current}%&7!" to console
		add 1 to {_error}
	if {_error} is set:
		broadcast "&6&lWolfo &7&l» &c%{_error}% errors were encountered while loading."
		broadcast "&7Please consult the console for more information."
		stop
	broadcast join wolfoGet("PREFIX.GENERAL"), wolfoGet("MESSAGE.SUCCESS_LOADING") and "&7(V%{_current}%)"
	set {wolfo::game::day} to 0
	set {wolfo::game::status} to "&6Attente"
	wolfoAutoMsg()

function wolfoGet(request: text) :: text: # POUR LES TEXTES (plus utilisé)
	broadcast "&8[YAML &7CONFIG&8]: &8REQ:&7%{_request}% &f- &8TYPE:&7TEXT" if {wolfo::game::debug::yml} is true
	return yaml value {_request} from "config"

function wolfoGetO(request: text) :: objects: # POUR LES OBJECTS
	broadcast "&8[YAML &7CONFIG&8]: &8REQ:&7%{_request}% &f- &8TYPE:&7OBJECTS" if {wolfo::game::debug::yml} is true
	return yaml list {_request} from "config"

function wolfoSet(request: text, value: text):
	broadcast "&8[YAML &7CONFIG&8]: &8REQ:&7%{_request}% &f- &8TYPE:&7TEXT" if {wolfo::game::debug::yml} is true
	set yaml value {_request} from "config" to {_value}
	save yaml "config"

function wolfoSetO(request: text, value: objects):
	broadcast "&8[YAML &7CONFIG&8]: &8REQ:&7%{_request}% &f- &8TYPE:&7OBJECTS" if {wolfo::game::debug::yml} is true
	set yaml value {_request} from "config" to {_value::*}
	save yaml "config"