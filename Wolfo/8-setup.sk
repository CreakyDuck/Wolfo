on script load:
	send join "&6&lWolfo &7&l» &8Took %difference between now and {wolfo::loading}%." to ops
	send "" to ops
	send "&6&lWolfo &7&l» &7Reloading setup ..." to ops
	set {wolfo::loading} to now

function wolfoMenuAction(p: player, loop: integer, action: text):
	close {_p}'s inventory
	if {_action} is "MANAGE":
		if {_loop} is 11:
			wolfoCheckStart({_p})
		else if {_loop} is 13:
			wolfoSet("GAME.PRIVACY", "CLOSED")
			loop all players:
				message join wolfoGet("PREFIX.GENERAL", loop-player) and wolfoGet("MESSAGE.PRIVACY.CLOSED", loop-player) to loop-player
		else if {_loop} is 14:
			wolfoSet("GAME.PRIVACY", "WHITELISTED")
			loop all players:
				message join wolfoGet("PREFIX.GENERAL", loop-player) and wolfoGet("MESSAGE.PRIVACY.WHITELISTED", loop-player) to loop-player
		else if {_loop} is 15:
			wolfoSet("GAME.PRIVACY", "OPEN")
			loop all players:
				message join wolfoGet("PREFIX.GENERAL", loop-player) and wolfoGet("MESSAGE.PRIVACY.OPEN", loop-player) to loop-player
	else:
		if {_loop} is 11:
			wolfoSetL("GAME.LOCATIONS.MIDDLE", position of {_p})
			send join wolfoGet("PREFIX.GENERAL", {_p}), wolfoGet("MESSAGE.LOCATIONSET", {_p}) and "&7(%position of {_p}%)" to {_p}
			wolfoUpdateO("GAME.SETUP.STEPS", "SETUP.MIDDLE", 0)
		else if {_loop} is 12:
			set {wolfo::setup::pos} to 1 if {wolfo::setup::pos} is not set
			wolfoSetL("GAME.LOCATIONS.SPAWNPOINTS.%{wolfo::setup::pos}%", position of {_p})
			send join wolfoGet("PREFIX.GENERAL", {_p}), wolfoGet("MESSAGE.LOCATIONSETO", {_p}) and "&7(%position of {_p}%)" to {_p}
			add 1 to {wolfo::setup::pos}
			if {wolfo::setup::pos} is 5:
				send join wolfoGet("PREFIX.GENERAL", {_p}) and wolfoGet("MESSAGE.SETUP_FINISHED1", {_p}) to {_p}
				wolfoUpdateO("GAME.SETUP.STEPS", "SETUP.SPAWNPOINT", 0)
		else if {_loop} is 14:
			wolfoSetL("GAME.LOCATIONS.BEGIN", position of {_p})
			send join wolfoGet("PREFIX.GENERAL", {_p}), wolfoGet("MESSAGE.LOCATIONSET", {_p}) and "&7(%position of {_p}%)" to {_p}
			wolfoUpdateO("GAME.SETUP.STEPS", "SETUP.BEGIN", 0)		
		else if {_loop} is 15:
			wolfoSetL("GAME.LOCATIONS.END", position of {_p})
			send join wolfoGet("PREFIX.GENERAL", {_p}), wolfoGet("MESSAGE.LOCATIONSET", {_p}) and "&7(%position of {_p}%)" to {_p}
			wolfoUpdateO("GAME.SETUP.STEPS", "SETUP.END", 0)
		set {_size} to size of wolfoGetO("GAME.SETUP.STEPS")
		if size of wolfoGetO("GAME.SETUP.STEPS") is 4:
			wolfoSet("GAME.SETUP.FINISH", "true")
			send join wolfoGet("PREFIX.GENERAL", {_p}) and wolfoGet("MESSAGE.SETUP_FINISHED2", {_p}) to {_p}
			wolfoDraw("center", "")