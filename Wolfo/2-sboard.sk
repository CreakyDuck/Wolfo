#
# This file is part of Wolfo.
#
# Wolfo is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# Wolfo is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with Wolfo.  If not, see <https://www.gnu.org/licenses/>.
#
# Copyright 2020 - Romitou <romitou@protonmail.com> and contributors
#

on script load:
	send join "&3&lWolfo &7&l▪ &8Took %difference between now and {wolfo::loading}%." to ops
	send "" to ops
	send "&3&lWolfo &7&l▪ &7Loading scoreboards ..." to ops
	set {wolfo::loading} to now

function wolfoSbPlayer(p: player, i: integer):
	wolfoDebug("Update scoreboard to %{_p}% with type %{_i}%")
	if {_i} is 0:
		set title of {_p}'s scoreboard to wolfoGetText("PREFIX.TITLE", 1)
		set line 12 of {_p}'s scoreboard to "&1"
		set line 11 of {_p}'s scoreboard to "&3&l» &f&lPartie:"
		set line 10 of {_p}'s scoreboard to "&2"
		set line 5 of {_p}'s scoreboard to "&3"
		set line 4 of {_p}'s scoreboard to "&3&l» &f&lJoueur:"
		set line 3 of {_p}'s scoreboard to "&4"
	if {_i} is 1:
		clear scoreboard of {_p}
	wait 1 tick # Obligatoire pour mettre à jour le scoreboard :'(
	# Sera fixé dans une des prochaines versions.
	wolfoSb(all players)

function wolfoSb(p: objects):
	wolfoDebug("Update scoreboard to %{_p::*}%")
	set {_status} to {wolfo::game::status}
	set {_day} to {wolfo::game::day}
	loop {_p::*}:
		set {_stat} to wolfoGetText("MESSAGE.TIME.%{_status}%", 2, loop-value)
		set {_config} to wolfoGetText("MESSAGE.GAMETYPE.%wolfoGetConfig()%", 2, loop-value)
		set line 9 of loop-value's scoreboard to " &3● &bStatut&f&l: %{_stat}%"
		set {_players} to size of players if {wolfo::game::status} is "WAIT", else size of {wolfo::players::alive::*}
		set line 8 of loop-value's scoreboard to " &3● &bConnectés&f&l: &7%{_players}% joueur(s)"
		set line 7 of loop-value's scoreboard to " &3● &bConfiguration&f&l: %{_config}%"
		set line 6 of loop-value's scoreboard to " &3● &bJour&f&l: &7n°%{_day}%"
		set line 2 of loop-value's scoreboard to " &3● &bVotre rôle&f&l: &e%{wolfo::players::role::%loop-value%}%"
		set line 1 of loop-value's scoreboard to " &3● &bVotre statut&f&l: &e%{wolfo::players::life::%loop-value%}%"
