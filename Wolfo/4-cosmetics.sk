# Cosmétiques - Romitou
function wolfoCosmetic(type: integer, p: player):
	if {_type} is 1 or 2:
		if {_type} is 1:
			set {_title} to wolfoGet("GUI.EFFECT_SELECT")
			add wolfoGetO("COSMETICS.EFFECT") to {_p::*}
		else if {_type} is 2:
			if {wolfo::cosmetic::%{_p}%::1} is "nyancat":  
				wolfoCosmetic(3, {_p})
				stop
			set {_title} to wolfoGet("GUI.PARTICLE_SELECT")
			add wolfoGetO("COSMETICS.PARTICLE") to {_p::*}
		WolfoMenuDeco({_p}, {_title}, 5)
		set {_loop} to 11
		loop {_p::*}:
			while {_loop} is 16, 17, 18, 26, 27 or 28:
				add 1 to {_loop}
			set {_value} to "%loop-value%"
			set {_value::*} to {_value} split at ";"
			make a gui slot {_loop} of {_p} with {_value::2} parsed as material named colored {_value::3} to run:
				set {wolfo::cosmetic::%{_p}%::%{_type}%} to {_value::1}
				close {_p}'s inventory
				add 1 to {_type}
				wolfoCosmetic({_type}, {_p})
			add 1 to {_loop}
	else if {_type} is 3:
		set {_particle} to {wolfo::cosmetic::%{_p}%::2}
		set {_type} to {wolfo::cosmetic::%{_p}%::1}
		set {wolfo::cosmetic::%{_p}%::active} to true
		if {_type} is "wings":
			set {_r} to random integer between 1 and 7 # La personnalisation de l'effet des ailes sera ajoutée dans une version future.
			drawWings style {_r}, particle1 "%{_particle}%", particle2 "%{_particle}%", particle3 "%{_particle}%", center {_p}, id "ww-%{_p}%", angle 110, height 0, space 0.2, visibleRange 30, tps 0, second 1
		if {_type} is "nyancat":
			drawNyanCat center {_p}, id "ww-%{_p}%", visibleRange 30, tps 0, second 0
		if {_type} is "rings":
			drawRings particle "%{_particle}%", center {_p}, id "ww-%{_p}%", randomRotation true, animated true, radius 1, ringCount 3, ringDensity 1, visibleRange 30
		if {_type} is "spiral":
			drawComplexSpiral particle "%{_particle}%", center {_p}, id "ww-%{_p}%", rainbowMode true, clockwise true, scan true, radius 1, density 50, height 6, effectMod .05, start 0, visibleRange 30
		set slot 0 of {_p} to a barrier named wolfoGet("ITEM.COSM_OFF")
		send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.COSM_ON") to {_p}
	else if {_type} is 4:
		clear {wolfo::cosmetic::%{_p}%::active}
		stopEffect "ww-%{_p}%"
		set slot 0 of {_p} to a enchanted book named wolfoGet("ITEM.COSM")
		send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.COSM_OFF") to {_p}
