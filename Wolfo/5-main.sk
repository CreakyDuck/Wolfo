# Ce script contient toutes les informations nécessaires pour faire fonctionner les autres scripts.
# Merci de ne pas modifier son nom afin de garder une hiérachie et charger les autres scripts convenablement.

on script load:
	send join "&6&lWolfo &7&l» &8Took %difference between now and {wolfo::loading}%." to ops
	send "" to ops
	send "&6&lWolfo &7&l» &7Loading main ..." to ops
	set {wolfo::loading} to now
	wolfoInitialize()

function wolfoInitialize():
	set {wolfo::game::status} to "WAIT" if {wolfo::game::status} is not set
	set {wolfo::game::day} to "0" if {wolfo::game::day} is not set

function wolfoHelp(msg: text, p: player):
	send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.%{_msg}%.TITLE") to {_p}
	set {_help::%{_msg}%::*} to wolfoGetO("MESSAGE.%{_msg}%.MSG")
	loop {_help::%{_msg}%::*}:
		send colored loop-value to {_p}

function wolfoJoin(p: player):
	set {_p}'s xp to 0
	teleport {_p} to wolfoGetL("GAME.LOCATIONS.BEGIN")
	set {_p}'s gamemode to adventure
	clear {_p}'s inventory
	heal {_p}
	reset {_p}'s walk speed
	set slot 0 of {_p} to enchanted book named wolfoGet("ITEM.COSM")
	set slot 4 of {_p} to book named wolfoGet("ITEM.INFO")
	set slot 8 of {_p} to orange bed named wolfoGet("ITEM.EXIT")
	set slot 6 of {_p} to magma cream named wolfoGet("ITEM.SETUP") if {_p} is op
	set slot 2 of {_p} to ender pearl named wolfoGet("ITEM.MANAGE") if {_p} is op
	set {wolfo::players::role::%{_p}%} to "&7Aucun"
	set {wolfo::players::life::%{_p}%} to "&aEn vie"
	wolfoSbPlayer({_p}, 0)
	wait 1 second
	send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.WELCOME") to {_p}

function wolfoLeave(p: player):
	wolfoSbPlayer({_p}, 1)
	remove {_p} from {wolfo::role::list::%{wolfo::players::role_name::%{_p}%}%::*}
	remove {_p} from {wolfo::players::list::*}
	remove {_p} from {wolfo::players::alive::*}

command /wolfo [<text>] [<text>] [<integer>]:
	trigger:
		if arg 1 is "help":
			wolfoHelp("COMMANDS", player)
		else if arg 1 is "setup":
			wolfoMenu(player, "SETUP")
		else if arg 1 is "manage":
			wolfoMenu(player, "MANAGE")
		else if arg 1 is "whitelist":
			wolfoWhitelist(player, arg-2, arg-3)
		else:
			wolfoHelp("COMMANDS", player)

function wolfoWhitelist(executor: player, target: text, action: integer):
	if {_executor} is not op:
		send join wolfoGet("PREFIX.GENERAL") and wolfoGet("PERMISSION.MESSAGE") to {_executor}
	else:
		set {_check::*} to wolfoGetO("GAME.WHITELIST")
		if {_action} is 1:
			if {_check::*} do not contain {_target}:
				wolfoUpdateO("GAME.WHITELIST", {_target}, 0)
				send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.WHITELISTED_SUCCESS") to {_executor}
			else:
				send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.WHITELISTED_ALREADY") to {_executor}
		else if {_action} is 2:
			if {_check::*} contains {_target}:
				wolfoUpdateO("GAME.WHITELIST", {_target}, 1)
				send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.NOT_WHITELISTED_SUCCESS") to {_executor}
			else:
				send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.NOT_WHITELISTED_ALREADY") to {_executor}
		else if {_action} is not set:
			if {_target} is set:
				if {_check::*} do not contain {_target}:
					send formatted join wolfoGet("PREFIX.GENERAL"), wolfoGet("MESSAGE.NOT_WHITELISTED") and "          <cmd:/wolfo whitelist %{_target}% 1><tooltip:&aAjouter %{_target}% la liste !>&a[Accorder l'accès à %{_target}%]&r" to {_executor}
				else:
					send formatted join wolfoGet("PREFIX.GENERAL"), wolfoGet("MESSAGE.WHITELISTED") and "          <cmd:/wolfo whitelist %{_target}% 2><tooltip:&cRetirer %{_target}% de la liste !>&c[Révoquer l'accès de %{_target}%]&r" to {_executor}
			else:
				send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.WHITELIST.LIST.1") to {_executor}
				if {_check::*} is not set:
					add "&8Aucun joueur." to {_check::*}
				loop {_check::*}:
					send "&e[&6%loop-index%&e] &f%loop-value%" to {_executor}
				send wolfoGet("MESSAGE.WHITELIST.LIST.2") to {_executor}

function wolfoCheckStart(p: player):
	set {_finish} to wolfoGet("GAME.SETUP.FINISH")
	if "%{_finish}%" is "true": # C'est moche, mais ça sera corrigé dans les prochaines versions. :)
		if {wolfo::debug::status} is true:
			wolfoTimer(5)
			stop
		if size of all players < 5:
			send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.NOTENOUGH") to {_p}
		else:
			send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.FORCESTART") to {_p}
			wolfoTimer(5)
			send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.START.PREPARE") to all players
	else:
		send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.MISSINGCONFIG") to {_p}

function wolfoStart():
	{wolfo::debug::status} is set:
		set {wolfo::players::list::*} to {wolfo::debug::list::*}
		set {wolfo::players::alive::*} to {wolfo::debug::list::*}
	else:
		set {wolfo::players::list::*} to players
		set {wolfo::players::alive::*} to players
	loop {wolfo::players::list::*}:
		teleport loop-value to {wolfo::spawn::%loop-index%}
		set loop-value's walk speed to 0
	wolfoGiveRoles()
	set {wolfo::game::automsg} to false
	set time of world "world" to 12:00
	send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.START.ROLES") to {wolfo::players::list::*}

function wolfoEnd():
	kick all players due to join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.ENDGAME")
	clear {wolfo::*}
	wolfoInitialize()

on join:
	if player is op:
		set join message to join wolfoGet("PREFIX.GENERAL"), "&e%player% ", wolfoGet("MESSAGE.JOINED") and " &8(&7%amount of all players%&8/&75&8)"
		wolfoJoin(player)
	else if size of all players is greater than or equal to 25:
		kick player due to join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.TOOMANY")
	else if {wolfo::game::start} is set:
		kick player due to join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.ALREADYSTART")
	else if wolfoGet("GAME.PRIVACY") is "CLOSED":
		kick player due to join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.PRIVACY.CLOSED")
	else if wolfoGet("GAME.PRIVACY") is "WHITELISTED":
		if wolfoGetO("GAME.WHITELIST") do not contain player:
			kick player due to join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.PRIVACY.WHITELISTED")
	else:
		set join message to join wolfoGet("PREFIX.GENERAL"), "&e%player% ", wolfoGet("MESSAGE.JOINED") and " &8(&7%amount of all players%&8/&75&8)"
		wolfoJoin(player)

on quit:
	set quit message to join wolfoGet("PREFIX.GENERAL"), "&e%player% ", wolfoGet("MESSAGE.LEFT") and " &8(&7%amount of all players%&8/&75&8)"
	wolfoLeave(player)

on inventory click:
	if name of event-item is wolfoGet("ITEM.COSM"):
		cancel event
		wolfoCosmetic(1, player)
	else if name of event-item is wolfoGet("ITEM.COSM_OFF"):
		cancel event
		wolfoCosmetic(4, player)
	else if name of event-item is wolfoGet("ITEM.INFO"):
		cancel event
		wolfoMenuRoles(player)
	else if name of event-item is wolfoGet("ITEM.VOTE"):
		wolfoVote(player)
	else if name of event-item is wolfoGet("ITEM.EXIT"):
		cancel event
		kick player due to join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.GOODLEFT")
	else if name of event-item is wolfoGet("ITEM.SETUP"):
		cancel event
		wolfoMenu(player, "SETUP")
	else if name of event-item is wolfoGet("ITEM.MANAGE"):
		cancel event
		wolfoMenu(player, "MANAGE")

on click:
	if name of player's tool is wolfoGet("ITEM.COSM"):
		cancel event
		wolfoCosmetic(1, player)
	else if name of player's tool is wolfoGet("ITEM.COSM_OFF"):
		cancel event
		wolfoCosmetic(4, player)
	else if name of player's tool is wolfoGet("ITEM.INFO"):
		cancel event
		wolfoMenuRoles(player)
	else if name of player's tool is wolfoGet("ITEM.VOTE"):
		wolfoVote(player)
	else if name of player's tool is wolfoGet("ITEM.EXIT"):
		cancel event
		kick player due to join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.GOODLEFT")
	else if name of player's tool is wolfoGet("ITEM.SETUP"):
		cancel event
		wolfoMenu(player, "SETUP")
	else if name of player's tool is wolfoGet("ITEM.MANAGE"):
		cancel event
		wolfoMenu(player, "MANAGE")

on drop:
	cancel event

on hunger meter change:
	cancel event

on weather change:
	cancel event

on damage of player:
	cancel event