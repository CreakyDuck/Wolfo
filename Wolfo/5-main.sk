# Ce script contient toutes les informations nécessaires pour faire fonctionner les autres scripts.
# Merci de ne pas modifier son nom afin de garder une hiérachie et charger les autres scripts convenablement.

on script load:
	send join "&6&lWolfo &7&l» &8Took %difference between now and {wolfo::loading}%." to ops
	send "" to ops
	send "&6&lWolfo &7&l» &7Loading main ..." to ops
	set {wolfo::loading} to now

function wolfoAutoMsg():
	set {_list::*} to wolfoGetO("MESSAGE.AUTO")
	if {wolfo::game::automsg} is not set:
		set {wolfo::game::automsg} to true
		while {wolfo::game::automsg} is true:
			set {_random} to a random element out of {_list::*}
			if {_random} is not {_last}:
				set {_last} to {_random}
				broadcast join wolfoGet("PREFIX.GENERAL"), "&7" and {_random}
				wait 30 seconds

function wolfoHelp(msg: text, p: player):
	send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.%{_msg}%_FIRST") to {_p}
	set {_help::%{_msg}%::*} to wolfoGetO("MESSAGE.%{_msg}%")
	loop {_help::%{_msg}%::*}:
		send colored loop-value to {_p}


function wolfoJoin(p: player):
	loop 200 times:
		send "" to {_p}
	set {_p}'s xp to 0
	teleport {_p} to {wolfo::loc::waitRoom}
	set {_p}'s gamemode to adventure
	clear {_p}'s inventory
	heal {_p}
	set {_p}'s walk speed to 0.2
	set {_p}'s selected hotbar slot to slot 4 of {_p}
	add {_p} to {wolfo::players::list::*}
	set slot 0 of {_p} to enchanted book named wolfoGet("ITEM.COSM")
	set slot 4 of {_p} to book named wolfoGet("ITEM.INFO")
	set slot 8 of {_p} to orange bed named wolfoGet("ITEM.EXIT")
	set slot 6 of {_p} to magma cream named wolfoGet("ITEM.SETUP") if {_p} is op
	set slot 2 of {_p} to ender pearl named wolfoGet("ITEM.MANAGE") if {_p} is op
	wolfoCheckStart()
	wolfoSbPlayer({_p}, 0)
	set {wolfo::players::role_text::%{_p}%} to "&7Aucun"
	set {wolfo::players::life::%{_p}%} to "&aEn vie"
	wait 1 second
	send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.JOIN_INFO") to {_p}

function wolfoLeave(p: player):
	wolfoSbPlayer({_p}, 1)
	remove {_p} from {wolfo::players::list::*}

function wolfoSetupGame(p: player):
	wolfoMenuSetup({_p})

command /wolfo [<text>] [<text>] [<integer>]:
	trigger:
		if arg 1 is "help":
			wolfoHelp("COMMANDS", player)
		else if arg 1 is "admin":
			wolfoSetupGame(player)
		else if arg 1 is "whitelist":
			if arg-3 is set:
				wolfoWhitelist(player, arg-2, arg-3)
			else:
				wolfoWhitelist(player, arg-2, 0)
		else:
			wolfoHelp("ADMIN", player)

function wolfoWhitelist(executor: player, target: text, action: integer):
	set {_check::*} to wolfoGetO("WHITELIST.PLAYERS")
	if {_action} is 0:
		if {_check::*} do not contain {_target}:
			send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.NOT_WHITELISTED") to {_executor}
			send join wolfoGet("PREFIX.GENERAL") and formatted "<cmd:/wolfo whitelist %{_target}% 1><tooltip:&aAjouter %{_target}% la liste !>&a[Accorder l'accès à %{_target}%]&r" to {_executor}
		else:
			send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.WHITELISTED") to {_executor}
			send join wolfoGet("PREFIX.GENERAL") and formatted "<cmd:/wolfo whitelist %{_target}% 2><tooltip:&cRetirer %{_target}% de la liste !>&c[Révoquer l'accès de %{_target}%]&r" to {_executor}
	else if {_action} is 1:
		if {_check::*} do not contain {_target}:
			wolfoUpdateO("WHITELIST.PLAYERS", {_target}, 0)
			send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.WHITELISTED_SUCCESS") to {_executor}
		else:
			send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.WHITELISTED_ALREADY") to {_executor}
	else if {_action} is 2:
		if {_check::*} contains {_target}:
			wolfoUpdateO("WHITELIST.PLAYERS", {_target}, 1)
			send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.NOT_WHITELISTED_SUCCESS") to {_executor}
		else:
			send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.NOT_WHITELISTED_ALREADY") to {_executor}

function wolfoCheckStart():
	if size of {wolfo::players::list::*} < 5:
		broadcast join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.NOTENOUGH")
	else:
		wolfoTime(30, {wolfo::players::list::*}, 0)
		send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.START_PREPARE") to {wolfo::players::list::*}

function wolfoStart():
	loop {wolfo::players::list::*}:
		teleport loop-value to {wolfo::spawn::%loop-index%}
		set loop-value's walk speed to 0
	wolfoGiveRoles()
	set {wolfo::game::automsg} to false
	send join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.START_ROLES") to {wolfo::players::list::*}

function wolfoEnd():
	loop {wolfo::players::list::*}:
		kick loop-value due to join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.ENDGAME")
	clear {wolfo::players::*}
	clear {wolfo::game::*}
	clear {wolfo::timer}
	clear {wolfo::vote::*}


on join:
	if {wolfo::game::start} is set:
		kick player due to join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.ALREADYSTART")
	else if size of {wolfo::players::list::*} is greater than or equal to 27:
		kick player due to join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.TOOMANY")
	else:
		set join message to "&a✔ %player% &7(%amount of all players% &8» %wolfoGetConfig(1)%&7)"
		wolfoJoin(player)

on quit:
	set quit message to "&c✘ %player% &7(%amount of all players - 1% &8» %wolfoGetConfig(1)%&7)"
	wolfoLeave(player)

on right click:
	if name of player's tool is wolfoGet("ITEM.COSM"):
		cancel event
		wolfoCosmetic(1, player)
	else if name of player's tool is wolfoGet("ITEM.COSM_OFF"):
		cancel event
		wolfoCosmetic(4, player)
	else if name of player's tool is wolfoGet("ITEM.INFO"):
		cancel event
		wolfoMenuRoles(player)
	else if name of player's tool is wolfoGet("ITEM.EXIT"):
		cancel event
		kick player due to join wolfoGet("PREFIX.GENERAL") and wolfoGet("MESSAGE.GOODLEFT")
	else if name of player's tool is wolfoGet("ITEM.SETUP"):
		cancel event
		wolfoMenuSetup(player)
	else if name of player's tool is wolfoGet("ITEM.MANAGE"):
		cancel event
		wolfoMenuManage(player)

on jump:
	{wolfo::game::start} is set
	cancel event

on drop:
	cancel event

on inventory click:
	if player isn't op:
		cancel event

on hunger meter change:
	cancel event

on weather change:
	cancel event

on damage of player:
	cancel event