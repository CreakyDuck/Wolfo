# Ce script contient toutes les informations nécessaires pour faire fonctionner les autres scripts.
# Merci de ne pas modifier son nom afin de garder une hiérachie et charger les autres scripts convenablement.

on script load:
	send join "&6&lWolfo &7&l» &8Took %difference between now and {wolfo::loading}%." to ops
	send "" to ops
	send "&6&lWolfo &7&l» &7Loading main ..." to ops
	set {wolfo::loading} to now
	wolfoInitialize()

function wolfoInitialize():
	set {wolfo::game::status} to "WAIT" if {wolfo::game::status} is not set
	set {wolfo::game::day} to "0" if {wolfo::game::day} is not set

function wolfoHelp(msg: text, p: player):
	send join wolfoGet("PREFIX.GENERAL", {_p}) and wolfoGet("MESSAGE.%{_msg}%.TITLE", {_p}) to {_p}
	set {_help::%{_msg}%::*} to wolfoGet("MESSAGE.%{_msg}%.MSG", {_p})
	loop {_help::%{_msg}%::*}:
		send colored loop-value to {_p}

function wolfoJoin(p: player):
	set {_p}'s xp to 0
	teleport {_p} to wolfoGetL("GAME.LOCATIONS.BEGIN")
	set {_p}'s gamemode to adventure
	clear {_p}'s inventory
	heal {_p}
	reset {_p}'s walk speed
	set slot 0 of {_p} to enchanted book named wolfoGet("ITEM.COSM", {_p})
	set slot 4 of {_p} to book named wolfoGet("ITEM.INFO", {_p})
	set slot 8 of {_p} to orange bed named wolfoGet("ITEM.EXIT", {_p})
	set slot 6 of {_p} to magma cream named wolfoGet("ITEM.SETUP", {_p}) if {_p} is op
	set slot 2 of {_p} to ender pearl named wolfoGet("ITEM.MANAGE", {_p}) if {_p} is op
	set {wolfo::players::role::%{_p}%} to "&7Aucun"
	set {wolfo::players::life::%{_p}%} to "&aEn vie"
	wolfoSbPlayer({_p}, 0)
	wait 1 second
	send join wolfoGet("PREFIX.GENERAL", {_p}) and wolfoGet("MESSAGE.WELCOME", {_p}) to {_p}

function wolfoLeave(p: player):
	wolfoSbPlayer({_p}, 1)
	remove {_p} from {wolfo::role::list::%{wolfo::players::role_name::%{_p}%}%::*}
	remove {_p} from {wolfo::players::list::*}
	remove {_p} from {wolfo::players::alive::*}

command /wolfo [<text>] [<text>] [<integer>]:
	trigger:
		if arg 1 is "help":
			wolfoHelp("COMMANDS", player)
		else if arg 1 is "setup":
			wolfoMenu(player, "SETUP")
		else if arg 1 is "manage":
			wolfoMenu(player, "MANAGE")
		else if arg 1 is "whitelist":
			wolfoWhitelist(player, arg-2, arg-3)
		else:
			wolfoHelp("COMMANDS", player)

function wolfoWhitelist(executor: player, target: text, action: integer):
	if {_executor} is not op:
		send join wolfoGet("PREFIX.GENERAL", {_executor}) and wolfoGet("MESSAGE.PERMISSION.MESSAGE", {_executor}) to {_executor}
	else:
		set {_check::*} to wolfoGetO("GAME.WHITELIST")
		if {_action} is 1:
			if {_check::*} do not contain {_target}:
				wolfoUpdateO("GAME.WHITELIST", {_target}, 0)
				send join wolfoGet("PREFIX.GENERAL", {_executor}) and wolfoGet("MESSAGE.WHITELISTED_SUCCESS", {_executor}) to {_executor}
			else:
				send join wolfoGet("PREFIX.GENERAL", {_executor}) and wolfoGet("MESSAGE.WHITELISTED_ALREADY", {_executor}) to {_executor}
		else if {_action} is 2:
			if {_check::*} contains {_target}:
				wolfoUpdateO("GAME.WHITELIST", {_target}, 1)
				send join wolfoGet("PREFIX.GENERAL", {_executor}) and wolfoGet("MESSAGE.NOT_WHITELISTED_SUCCESS", {_executor}) to {_executor}
			else:
				send join wolfoGet("PREFIX.GENERAL", {_executor}) and wolfoGet("MESSAGE.NOT_WHITELISTED_ALREADY", {_executor}) to {_executor}
		else if {_action} is not set:
			if {_target} is set:
				if {_check::*} do not contain {_target}:
					send formatted join wolfoGet("PREFIX.GENERAL", {_executor}), wolfoGet("MESSAGE.NOT_WHITELISTED", {_executor}) and "          <cmd:/wolfo whitelist %{_target}% 1><tooltip:&aAjouter %{_target}% la liste !>&a[Accorder l'accès à %{_target}%]&r" to {_executor}
				else:
					send formatted join wolfoGet("PREFIX.GENERAL", {_executor}), wolfoGet("MESSAGE.WHITELISTED", {_executor}) and "          <cmd:/wolfo whitelist %{_target}% 2><tooltip:&cRetirer %{_target}% de la liste !>&c[Révoquer l'accès de %{_target}%]&r" to {_executor}
			else:
				send join wolfoGet("PREFIX.GENERAL", {_executor}) and wolfoGet("MESSAGE.WHITELIST.LIST.1", {_executor}) to {_executor}
				if {_check::*} is not set:
					add "&8Aucun joueur." to {_check::*}
				loop {_check::*}:
					send "&e[&6%loop-index%&e] &f%loop-value%" to {_executor}
				send wolfoGet("MESSAGE.WHITELIST.LIST.2", {_executor}) to {_executor}

function wolfoCheckStart(p: player):
	set {_finish} to wolfoGetO("GAME.SETUP.FINISH")
	if "%{_finish}%" is "true": # C'est moche, mais ça sera corrigé dans les prochaines versions. :)
		if {wolfo::debug::status} is true:
			wolfoTimer(5)
			stop
		if size of all players < 5:
			send join wolfoGet("PREFIX.GENERAL", {_p}) and wolfoGet("MESSAGE.NOTENOUGH", {_p}) to {_p}
		else:
			if {wolfo::setup::pos} is greater than or equal to size of all players:
				send join wolfoGet("PREFIX.GENERAL", {_p}) and wolfoGet("MESSAGE.FORCESTART", {_p}) to {_p}
				wolfoTimer(5)
				send join wolfoGet("PREFIX.GENERAL", {_p}) and wolfoGet("MESSAGE.START.PREPARE", {_p}) to all players
			else:
				broadcast "Il n'y a pas assez de points de téléportation."
	else:
		send join wolfoGet("PREFIX.GENERAL", {_p}) and wolfoGet("MESSAGE.MISSINGCONFIG", {_p}) to {_p}

function wolfoStart():
	send "&6&lWolfo &7&l» &fWolfo a été développé par &eXamez##8497&f et &eRomitou##9685&f. Si vous avez la moindre question, rejoignez notre Discord : https://discordapp.com/invite/37nvkpk !" to all players
	{wolfo::debug::status} is true:
		set {wolfo::players::list::*} to {wolfo::debug::list::*}
		set {wolfo::players::alive::*} to {wolfo::debug::list::*}
		send "&6&lWolfo &7&l» &fLe &cmode développeur&f est activé. Le déroulement de cette partie sera en conséquent modifiée." to all players
		broadcast "%{wolfo::players::list::*}% %{wolfo::debug::list::*}%"
	else:
		set {wolfo::players::list::*} to players
		set {wolfo::players::alive::*} to players
	set {wolfo::spawn::*} to wolfoGetO("GAME.LOCATIONS.SPAWNPOINTS")
	loop {wolfo::players::list::*}:
		teleport loop-value to wolfoGetL("GAME.LOCATIONS.SPAWNPOINTS.%loop-index%")
		set loop-value's walk speed to 0
	wolfoGiveRoles()
	set {wolfo::game::automsg} to false
	set time of world "world" to 12:00
	loop {wolfo::players::list::*}:
		send join wolfoGet("PREFIX.GENERAL", loop-value) and wolfoGet("MESSAGE.START.ROLES", loop-value) to loop-value

function wolfoEnd():
	loop all players:
		kick loop-player due to join wolfoGet("PREFIX.GENERAL", loop-player) and wolfoGet("MESSAGE.ENDGAME", loop-player)
	clear {wolfo::*}
	wolfoInitialize()

on join:
	if player is op:
		set join message to ""
		loop all players:
			message join wolfoGet("PREFIX.GENERAL", loop-player), "&e%player% ", wolfoGet("MESSAGE.JOINED", loop-player) and " &8(&7%amount of all players%&8/&75&8)"
		wolfoJoin(player)
	else if size of all players is greater than or equal to 25:
		kick player due to join wolfoGet("PREFIX.GENERAL", player) and wolfoGet("MESSAGE.TOOMANY", player)
	else if {wolfo::game::start} is set:
		kick player due to join wolfoGet("PREFIX.GENERAL", player) and wolfoGet("MESSAGE.ALREADYSTART", player)
	else if wolfoGetO("GAME.PRIVACY") is "CLOSED":
		kick player due to join wolfoGet("PREFIX.GENERAL", player) and wolfoGet("MESSAGE.PRIVACY.CLOSED", player)
	else if wolfoGetO("GAME.PRIVACY") is "WHITELISTED":
		if wolfoGetO("GAME.WHITELIST") do not contain player:
			kick player due to join wolfoGet("PREFIX.GENERAL", player) and wolfoGet("MESSAGE.PRIVACY.WHITELISTED", player)
	else:
		set join message to ""
		loop all players:
			message join wolfoGet("PREFIX.GENERAL", loop-player), "&e%player% ", wolfoGet("MESSAGE.JOINED", loop-player) and " &8(&7%amount of all players%&8/&75&8)" to loop-player
		wolfoJoin(player)

on quit:
	set quit message to ""
	loop all players:
		message join wolfoGet("PREFIX.GENERAL", loop-player), "&e%player% ", wolfoGet("MESSAGE.LEFT", loop-player) and " &8(&7%amount of all players%&8/&75&8)" to loop-player
	wolfoLeave(player)

on inventory click:
	if name of event-item is wolfoGet("ITEM.COSM", player):
		cancel event
		wolfoCosmetic(1, player)
	else if name of event-item is wolfoGet("ITEM.COSM_OFF", player):
		cancel event
		wolfoCosmetic(4, player)
	else if name of event-item is wolfoGet("ITEM.INFO", player):
		cancel event
		wolfoMenuRoles(player)
	else if name of event-item is wolfoGet("ITEM.VOTE", player):
		wolfoVote(player)
	else if name of event-item is wolfoGet("ITEM.EXIT", player):
		cancel event
		kick player due to join wolfoGet("PREFIX.GENERAL", player) and wolfoGet("MESSAGE.GOODLEFT", player)
	else if name of event-item is wolfoGet("ITEM.SETUP", player):
		cancel event
		wolfoMenu(player, "SETUP")
	else if name of event-item is wolfoGet("ITEM.MANAGE", player):
		cancel event
		wolfoMenu(player, "MANAGE")

on click:
	if name of player's tool is wolfoGet("ITEM.COSM", player):
		cancel event
		wolfoCosmetic(1, player)
	else if name of player's tool is wolfoGet("ITEM.COSM_OFF", player):
		cancel event
		wolfoCosmetic(4, player)
	else if name of player's tool is wolfoGet("ITEM.INFO", player):
		cancel event
		wolfoMenuRoles(player)
	else if name of player's tool is wolfoGet("ITEM.VOTE", player):
		wolfoVote(player)
	else if name of player's tool is wolfoGet("ITEM.EXIT", player):
		cancel event
		kick player due to join wolfoGet("PREFIX.GENERAL", player) and wolfoGet("MESSAGE.GOODLEFT", player)
	else if name of player's tool is wolfoGet("ITEM.SETUP", player):
		cancel event
		wolfoMenu(player, "SETUP")
	else if name of player's tool is wolfoGet("ITEM.MANAGE", player):
		cancel event
		wolfoMenu(player, "MANAGE")

on drop:
	cancel event

on hunger meter change:
	cancel event

on weather change:
	cancel event

on damage of player:
	cancel event
