# Ce script contient toutes les informations nécessaires pour faire fonctionner les autres scripts.
# Merci de ne pas modifier son nom afin de garder une hiérachie et charger les autres scripts convenablement.

on script load:
	send join "&6&lWolfo &7&l» &8Took %difference between now and {wolfo::loading}%." to ops
	send "" to ops
	send "&6&lWolfo &7&l» &7Loading GUI ..." to ops
	set {wolfo::loading} to now


# Cette partie va bientôt être recodée.
# Bug connu: clignotement de l'inventaire (comportement normal)
function wolfoMenuDeco(p: player, t: text, i: integer = 6):
	if {wolfo::vote::open} is true:
		if {wolfo::role::list::TOWNSFOLK::*} do not contain {_p}:
			open virtual chest inventory with {_i} rows named wolfoGet("ROLES.TOWNSFOK.NAME") to {_p}
			add {_p} to {wolfo::role::list::TOWNSFOLK::*}
			wolfoMenuVote("TOWNSFOLK")
	else:
		open virtual chest inventory with {_i} rows named {_t} to {_p}
	if {_i} = 3:
		make a gui slot 1, 2, 3, 4, 5, 6, 7, 9, 10, 16, 17, 19, 20, 21, 22, 23, 24 and 25 of {_p} with magenta stained glass pane named "&f"
		make a gui slot 0, 8, 18 and 26 of {_p} with purple stained glass pane named "&f"
	if {_i} = 5:
		make a gui slot 1, 2, 3, 4, 5, 6, 7, 9, 10, 16, 17, 18, 26, 27, 28, 34, 35, 37, 38, 39, 40, 41, 42 and 43 of {_p} with magenta stained glass pane named "&f"
		make a gui slot 0, 8, 36 and 44 of {_p} with purple stained glass pane named "&f"
	if {_i} = 6:
		make a gui slot 1, 2, 3, 4, 5, 6, 7, 9, 10, 16, 17, 18, 26, 27, 35, 36, 37, 43, 44, 45, 46, 47, 48, 49, 50, 51 and 52 of {_p} with magenta stained glass pane named "&f"
		make a gui slot 0, 8, 45 and 53 of {_p} with purple stained glass pane named "&f"

function wolfoMenuRoles(p: player):
	set {_title} to wolfoGet("GUI.ROLES")
	wolfoMenuDeco({_p}, {_title}, 5)
	set {_loop} to 11
	loop yaml node list "ROLES" from "config":
		while {_loop} is 16, 17, 18, 26, 27 or 28:
			add 1 to {_loop}
		set {_item} to wolfoGet("%loop-node%.ITEM") parsed as material
		if {_item} is a player head:
			set {_nbt} to wolfoGet("%loop-node%.NBT")
			add wolfoGet("%loop-node%.NBT") to nbt of {_item}
		set {_lore::*} to wolfoGetO("%loop-node%.DESC")
		make a gui slot {_loop} of {_p} with {_item} named colored wolfoGet("%loop-node%.NAME") with lore colored {_lore::*}
		add 1 to {_loop}
		wait 2 ticks

on inventory close:
	remove player from {wolfo::role::list::TOWNSFOLK::*}

function wolfoMenuVote(role: text):
	set {_name} to wolfoGet("ROLES.%{_role}%.NAME")
	loop {wolfo::role::list::%{_role}%::*}:
		unformat all gui slots of loop-value-1
		wolfoMenuDeco(loop-value-1, {_name}, 6)
		set {_loop} to 11
		loop {wolfo::players::alive::*}:
			while {_loop} is 16, 17, 18, 26, 27 or 28:
				add 1 to {_loop}
			set {_voter} to loop-value-1
			set {_target} to loop-value-2
			# CUPID -------------------------------------------
			if {_role} is "CUPID":
				if {wolfo::vote::voters::%{_voter}%::*} do not contain {_target}:
					set {_material} to {_target}'s skull named "&6%{_target}% &f- &7❤︎" with lore "" and ""
				else:
					set {_material} to {_target}'s skull named "&6%{_target}% &f- &c❤︎" with lore "" and ""
				make a gui slot {_loop} of {_voter} with {_material} to run:
					if {wolfo::vote::voters::%{_voter}%::*} contains {_target}:
						remove {_target} from {wolfo::vote::voters::%{_voter}%::*}
					else:
						add {_target} to {wolfo::vote::voters::%{_voter}%::*}
				make a gui slot 55 of {_voter} with light green dye named "&aTerminer" to run:
					close {_voter}'s inventory
					set {wolfo::timer} to 0
					set {wolfo::results::CUPID::*} to {wolfo::vote::voters::%{_voter}%::*}
			# WEREWOLF -------------------------------------------
			else if {_role} is "WEREWOLF" or "TOWNSFOLK":
				if {wolfo::vote::players::%{_target}%::*} is not set:
					set {_material} to {_target}'s skull named "&6%{_target}% &f- &7Aucun vote" with lore "" and "&7Personne n'a voté."
				else:
					set {_size} to size of {wolfo::vote::players::%{_target}%::*}
					if {_size} = 1:
						set {_vote} to "vote"
					else:
						set {_vote} to "votes"
					set {_material} to {_target}'s skull named "&6%{_target}% &f- &e%size of {wolfo::vote::players::%{_target}%::*}% %{_vote}%" with lore "" and "&7Votants: %{wolfo::vote::players::%{_target}%::*}%"
				make a gui slot {_loop} of {_voter} with {_material} to run:
					if {wolfo::vote::voters::%{_voter}%} is not set:
						set {wolfo::vote::voters::%{_voter}%} to {_target}
					else:
						remove {_voter} from {wolfo::vote::players::%{wolfo::vote::voters::%{_voter}%}%::*}
						remove {_target} from {wolfo::vote::list::*}
					add {_voter} to {wolfo::vote::players::%{_target}%::*}
					add {_target} to {wolfo::vote::list::*}
					set {wolfo::vote::voters::%{_voter}%} to {_target}
					send "&6&lWolfo &7&l» &rTon vote contre &e%{_target}%&f a été &aenregistré&f. J'espère que tu as fait le bon choix." to {_voter}
					add player to {wolfo::bypass::*}
					wolfoMenuVote({_role})
			# DOCTOR -------------------------------------------
			else if {_role} is "DOCTOR":
				make a gui slot {_loop} of {_voter} with {_target}'s skull named "&6%{_target}%" with lore "", "&7Protégez &e%{_target}% du", "&7vote des &cLoup-Garous&7." and "" to run:
					close {_voter}'s inventory
					set {wolfo::timer} to 0
					set {wolfo::results::DOCTOR} to {_target}
					send "Ayé!" to {_voter}
			# SEER -------------------------------------------
			else if {_role} is "SEER" or "SEER_AURA":
				if {_role} is "SEER":
					set {_type} to "l'aura"
				else:
					set {_type} to "le rôle"
				make a gui slot {_loop} of {_voter} with {_target}'s skull named "&6%{_target}%" with lore "", "&7Découvrez %{_type}% de &e%{_target}%", "&7en cliquant ici ! :D" and "" to run:
					close {_voter}'s inventory
					if {_role} is "SEER":
						set {_reveal} to {wolfo::players::role::%{_target}%}
					else if {_role} is "SEER_AURA":
						set {_aura} to wolfoGet("ROLES.%{wolfo::players::role::%{_target}%}%.AURA")
						set {_teams::*} to wolfoGetO("TEAMS")
						set {_reveal} to {_teams::%{_aura}%}
					send "Ui, %{_type}% de &e%{_target}% est %{_reveal}%" to {_voter}
					set {wolfo::vote::voters::%{_voter}%} to true
					set {wolfo::results::%{_role}%} to {_reveal}
					set {wolfo::timer} to 0
			# WITCH -------------------------------------------
			else if {_role} is "WITCH":
				set {_target} to {wolfo::results::WEREWOLF}
				make a gui slot 30 of {_voter} with {_target}'s skull named "&6%{_target}%" with lore "", "&7Protégez &e%{_target}% du", "&7vote des &cLoup-Garous&7." and ""
				if {wolfo::potions::SAVE} is not set:
					make a gui slot 38 of {_voter} with light green dye named "&aSélectionner cette option" with lore "", "&7Vous utiliserez une de vos", "&7deux potions en faisant cela." and "" to run:
						close {_voter}'s inventory
						set {wolfo::timer} to 0
						set {wolfo::results::WITCH} to 0
						set {wolfo::potions::SAVE} to true
				else:
					make a gui slot 38 of {_voter} with red dye named "&cPotion déjà utilisée" with lore "", "&7Vous utiliserez une de vos", "&7deux potions en faisant cela." and "" to run:
			add 1 to {_loop}

function wolfoMenu(p: player, type: text):
	if {_p} is not op:
		send join wolfoGet("PREFIX.GENERAL") and wolfoGet("PERMISSION.MESSAGE") to {_p}
	else:
		set {_title} to wolfoGet("GUI.%{_type}%")
		wolfoMenuDeco({_p}, {_title}, 3)
		set {_loop} to 11
		if {_type} is "SETUP":
			set {_steps::*} to wolfoGetO("GAME.SETUP.STEPS")
		loop yaml node list "%{_type}%" from "config":
			while {_loop} is 16, 17, 18, 26, 27 or 28:
				add 1 to {_loop}
			set {_item} to wolfoGet("%loop-node%.ITEM") parsed as material
			set {_lore::*} to wolfoGetO("%loop-node%.LORE")
			if {_type} is "SETUP":
				if {_steps::*} contains "%loop-node%":
					add "&2✔ &aTerminé !" and "" to {_lore::*}
			make a gui slot {_loop} of {_p} with {_item} named colored wolfoGet("%loop-node%.NAME") with lore colored {_lore::*} to run:
				wolfoMenuAction({_p}, {_loop}, {_type})
			add 1 to {_loop}
			wait 2 ticks